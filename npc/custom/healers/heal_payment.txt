//===== rAthena Script =======================================
//= Healer (with payment)
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= NPC heals/regenerates people against zenys
//===== Additional Comments: =================================
//= alternative dog with payment.
//= each HP need 1 zeny
//= each SP need (baselevel / 5) zenys
//= calculation is done at start of the script
//= Added Duplicates And Correct payon Loc [Darkchild]
//= Some modifications. Added NPC locations. [massdriller]
//= Removed Duplicates [Silent]
//============================================================
//= Изменён и адаптирован для сервера RO Fenrir
//============================================================

-	script	healer2	-1,{
	set @tempHp, MaxHp-Hp;
	set @tempSpReal, MaxSp-Sp;
	set @tempSp, ((MaxSp-Sp) * BaseLevel) / 5;
	if (@tempHp > 0) goto WOUNDED;
	if (@tempSpReal > 0) goto ONLY_REGENERATION;
	
	mes "[Healer]";
	mes "Ты не похож на больного.";
	close;

WOUNDED:
	if (@tempSp > 0) goto WITH_REGENERATION;
	mes "[Healer]";
	mes "Хреново выглядишь.";
	mes "Я могу помочь тебе за "+@tempHp+" зен.)";
	mes "Устраивает?";
	next;
	menu "Лечи ("+@tempHp+"z)",HEALINGS,"Не лечи",CANCEL;

WITH_REGENERATION:
	mes "[Healer]";
	mes "Я могу восстановить только HP за "+@tempHp+" зен,";
	mes "Или только SP за "+@tempSp+" зен,";
	mes "Или всё и сразу всего за "+(@tempHp+@tempSp)+" зен.";
	next;
	menu "Только HP ("+@tempHp+"z)",HEALINGS,"Только SP ("+@tempSp+"z)",REGENERATION,"Всё ("+(@tempHp+@tempSp)+"z)",HEALINGS_AND_REGEN,"Нет, спасибо",CANCEL;

ONLY_REGENERATION:
	mes "[Healer]";
	mes "Хочешь востановить только SP? ("+@tempSp+" z)";
	next;
	menu "Восстанови ("+@tempSp+"z)",REGENERATION,"Нет, спасибо",CANCEL;

HEALINGS:
	if (Zeny < @tempHp) goto NO_ZENYS;
	set Zeny, Zeny-@tempHp;
	heal @tempHp,0;
	goto FIN;

REGENERATION:
	if (Zeny < @tempSp) goto NO_ZENYS;
	set Zeny, Zeny-@tempSp;
	heal 0,@tempSpReal;
	goto FIN;

HEALINGS_AND_REGEN:
	if (Zeny < (@tempHp+@tempSp)) goto NO_ZENYS;
	set Zeny, Zeny-(@tempHp+@tempSp);
	heal @tempHp,@tempSpReal;
	goto FIN;

NO_ZENYS:
	mes "[Healer]";
	mes "У тебя не хватает денег.";
	mes "Приходи в другой раз.";
	close;

FIN:
	mes "[Healer]";
	mes "Ну всё, как новенький.";
	close;

CANCEL:
	mes "[Healer]";
	mes "Отлично, приходи ещё если что.";
	close;
}

// NPC Duplicates
alberta,185,144,5	duplicate(healer2)	Healer#alb	402
aldebaran,134,123,5	duplicate(healer2)	Healer#alde	402
amatsu,200,80,5	duplicate(healer2)	Healer#ama	402
ayothaya,155,111,5	duplicate(healer2)	Healer#ayo	402
comodo,188,162,5	duplicate(healer2)	Healer#com	402
geffen,123,60,5	duplicate(healer2)	Healer#gef	402
gonryun,164,130,5	duplicate(healer2)	Healer#gon	402
izlude,125,118,5	duplicate(healer2)	Healer#izl	402
louyang,225,103,5	duplicate(healer2)	Healer#lou	402
morocc,159,96,5	duplicate(healer2)	Healer#mor	402
niflheim,188,180,5	duplicate(healer2)	Healer#nif	402
payon,180,105,5	duplicate(healer2)	Healer#pay	402
prontera,150,184,5	duplicate(healer2)	Healer#pront	402
umbala,94,162,5	duplicate(healer2)	Healer#umb	402
xmas,149,136,5	duplicate(healer2)	Healer#xmas	402
yuno,146,184,5	duplicate(healer2)	Healer#yuno	402
