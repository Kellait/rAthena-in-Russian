//===== rAthena Script ======================================= 
//= Super Novice Job Quest
//===== By: ================================================== 
//= Darkchild
//===== Current Version: ===================================== 
//= 2.4
//===== Compatible With: ===================================== 
//= rAthena Project; SVN r15340+
//===== Description: ========================================= 
//= [Official Conversion]
//= Super Novice Job Change Npc
//= Super Novice Cart Rental Npc
//= Some Other Useless Super Novice Npc
//===== Additional Comments: ================================= 
//= 1.9 Rescripted to the Aegis 10.3 Standars. [Samuray22]
//= 2.0 Rescripted again, this time using an iRO base. [L0ne_W0lf]
//= 2.1 Swapped use of baseclass for basejob. [L0ne_W0lf]
//= 2.2 Added Quest Log commands. [Kisuka]
//= 2.3 NPC "Esseray" calls Ex. Super Novice script when enabled. [Euphy]
//= 2.4 Added an Ex. Super Novice dialog check. [Euphy]
//============================================================ 
//== Перевод Akaineko 
//== Корректор Fenrir
//============================================================

aldeba_in,223,167,3	script	Tzerero#sn	709,{
	if (Class == Job_SuperNovice || Class == Job_Super_Baby || Class == Job_Super_Novice_E || Class == Job_Super_Baby_E) {
		mes "[Tzerero]";
		mes "Я верю, что тебе нравится";
		mes "жизнь Супер-Новичка. Ах,";
		mes "отлично, отлично... Как я и ожидал.";
		mes "Действительно, свет посредственности сияет внутри тебя...";
		next;
		mes "[Tzerero]";
		mes "Кажется, мне удалось вдохновить тебя на жизнь";
		mes "подобно мистеру Киму-Шауну...";
		mes "Стать мастером на все руки...";
		mes "...не профессионалом ни в чём.";
		close;
	}
	if (countitem(938) > 29 && countitem(907) > 29 && SUPNOV_Q == 1) {
		delitem 938,30; //Sticky_Mucus
		delitem 907,30; //Resin
		mes "[Tzerero]";
		mes "А, ты принёс";
		mes "вещи, о которых я просил!";
		mes "Ты доказал,";
		mes "что досто вступить";
		mes "в общество Супер-Новичков.";
		next;
		mes "[Tzerero]";
		mes "Теперь я, как и обещал,";
		mes "я сделаю тебя";
		mes "^3355FFСупер-Но..^000000М?";
		mes "Что это позади тебя?";
		next;
		select("М?:Что там?");
		mes "^3355FFТы оглядываешься, но...";
		mes "Там ничего нет.";
		mes "Что-то странное здесь творится!^000000";
		next;
		completequest 6010;
		callfunc "F_ClearJobVar";
		getitem 2339,1; //G_Strings
		if (Upper==0) jobchange Job_SuperNovice;
		if (Upper==2) jobchange Job_Super_Baby;
		mes "[Tzerero]";
		mes "Хаха! Попал" + (Sex?"ся":"ась") + "!";
		mes "Как тебе шутка?";
		mes "Ладно, проехали...";
		next;
		mes "[Tzerero]";
		mes "Запомни, взрослый человек ";
		mes "вроде меня всегда носит ";
		mes "с собой запасную ";
		mes "пару трусов. ";
		next;
		mes "[Tzerero]";
		mes "Однако более важно то...";
		mes "Что тебе удалось приблизиться к ";
		mes "великим Супер-Новичкам.";
		next;
		mes "[Tzerero]";
		mes "Прими эти трусы в подарок...";
		mes "По слухам, такие же носил";
		mes "сам мистер Киму-Шаун,";
		mes "легендарный основатель нашего клуба,";
		mes "в ранний период своей жизни,";
		mes "когда он подавал пример посредственности.";
		next;
		mes "[Tzerero]";
		mes "Иди и наслаждайся своей новой жизнью";
		mes "Супер-Новичка! Всегда стремись ";
		mes "вперёд и помогай простым людям,";
		mes "будучи одним из них!";
		close;
	}
	if (SUPNOV_Q == 1) {
		mes "[Tzerero]";
		mes "М? Забыл" + (Sex?"":"а") + ", о чём я";
		mes "тебя просил? Окей,";
		mes "напомню ещё раз,";
		mes "постарайся не забыть...";
		next;
		mes "[Tzerero]";
		mes "Я просил принести мне";
		mes "^FF000030 'Sticky Mucus'^000000 и";
		mes "^FF000030 'Resin'^000000.";
		close;
	}
	if (Class == Job_Novice && Upper != 1) {
		mes "[Tzerero]";
		mes "...Хмм?";
		mes "Стой.";
		mes "Дай-ка взглянуть на тебя.";
		next;
		mes "[Tzerero]";
		mes ".....";
		mes ".......";
		mes ".........";
		emotion e_dots;
		next;
		mes "[Tzerero]";
		mes "Я вижу свет ";
		mes "посредственности,";
		mes "ярко сияющий в твоих глазах...";
		next;
		mes "[Tzerero]";
		mes "Не хочешь ли присоединиться к нам,";
		mes "молодой Новичок? Присоединяйся";
		mes "и познай тонкое";
		mes "великолепие бытия";
		mes "посредственности...";
		next;
		mes "[Tzerero]";
		mes "Прими моё предложение...";
		mes "Отбрось свой пыльный,";
		mes "тусклый образ";
		mes "и расцвети как...";
		next;
		mes "[Tzerero]";
		mes "...^CE6300Супер-Новичок^000000.";
		next;
		switch(select("Принять предложение.:Отклонить предложение.:Прислушаться.")) {
		case 1:
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Tzerero]";
				mes "Хмм...Действительно ли ты";
				mes "понимаешь ценность";
				mes "силы, заключённой в";
				mes "слабости? Ты долж" + (Sex?"ен":"на") + " будешь";
				mes "доказать, что ты - ";
				mes "настоящий отстающий.";
				next;
				mes "[Tzerero]";
				mes "Проживи жизнь Новичка...";
				mes "И возвращайся, когда";
				mes "выучишь все";
				mes "основные умения...";
				mes "Развивай в себе заурядность и";
				mes "стань Новичком профессионального 10 уровня...";
				close;
			}
			if (BaseLevel < 45) {
				mes "[Tzerero]";
				mes "Хмм... Но действительно ли ты";
				mes "ценишь расслабленный образ жизни";
				mes "банального приключенца?";
				mes "Докажи мне, что ты не";
				mes "жаждешь могущества...";
				next;
				mes "[Tzerero]";
				mes "Поживи мирской жизнью...";
				mes "Стань Новичком 45 базового уровня...";
				mes "Тогда ты сможешь присоединиться к нам...";
				close;
			}
			mes "[Tzerero]";
			mes "Я вижу в твоих глазах";
			mes "стремление";
			mes "жить простой жизнью...";
			next;
			mes "[Tzerero]";
			mes "только настоящий мудрец может";
			mes "сказать, что быть заурядным";
			mes "и банальным - это лучший";
			mes "способ прожить жизнь.";
			next;
			mes "[Tzerero]";
			mes "Однако мы не принимаем";
			mes "кого попало в своё Общество.";
			mes "Ты долж" + (Sex?"ен":"на") + " будешь пройти";
			mes "тест на квалификацию.";
			next;
			mes "[Tzerero]";
			mes "Для этого тебе нужно будет";
			mes "принести мне кое-какие предметы,";
			mes "которые добываются с обычных,";
			mes "ничем не примечательных монстров.";
			next;
			mes "[Tzerero]";
			mes "Хмммм...";
			mes "^FF000030 'Sticky Mucus'^000000";
			mes "и ^FF000030 'Resin'";
			mes "^000000подойдут для проверки твоей способности";
			mes "сражаться с мелкими врагами.";
			next;
			mes "[Tzerero]";
			mes "Да, число 30 значимо.";
			mes "Это не какое-то";
			mes "супер-особеное число... Это ";
			mes "просто обыкновенное число.";
			mes "Ха-ха-ха-ха~";
			next;
			set SUPNOV_Q,1;
			setquest 6010;
			mes "[Tzerero]";
			mes "Удачи, мой друг.";
			close;
		case 2:
			mes "[Tzerero]";
			mes "Ну да, ну да... Полагаю,";
			mes "тебе трудно оценить";
			mes "прелесть заурядной жизни.";
			mes "Что ж... Это нормально...";
			mes "Твоя жизнь - это твоя жизнь.";
			next;
			mes "[Tzerero]";
			mes "Но если однажды";
			mes "ты узришь свет банальности, знай:";
			mes "ты - мой желанный гость";
			mes "в любое время...";
			next;
			mes "[Tzerero]";
			mes "С ростом популярности";
			mes "нашего Клуба Новичков";
			mes "и увеличением числа наших последователей";
			mes "мы можем решить ввести";
			mes "более трудный тест...";
			close;
		case 3:
			mes "[Tzerero]";
			mes "Наше Общество Новичков было ";
			mes "основано легендарным Мистером ";
			mes "Киму-Шауном... Наверное,";
			mes "величайшим из людей в истории.";
			next;
			mes "[Tzerero]";
			mes "Он осознал, что в Мире";
			mes "так много страдания,";
			mes "особенно среди";
			mes "простых людей";
			mes "Рун-Мидгарда...";
			next;
			mes "[Tzerero]";
			mes "Он выучился множеству умений ";
			mes "у различных людей, которых ";
			mes "встречал... Но, не занимаясь ";
			mes "одним и тем же подолгу, ";
			mes "он стал мастером на все руки...";
			mes "И мастером ни в чём.";
			next;
			mes "Разделяя боль ";
			mes "обычных людей, он стал ";
			mes "одним из них...";
			mes "Величайшим простым ";
			mes "человеком.";
			next;
			mes "[Tzerero]";
			mes "Члены нашего общества ";
			mes "пытаются жить как мистер ";
			mes "Киму-Шаун, согласно принципам, ";
			mes "которые он сформулировал до нас...";
			close;
		}
	}
	else {
		if (Class == Job_SuperNovice || Class == Job_Super_Baby || Class == Job_Super_Novice_E || Class == Job_Super_Baby_E) {
			mes "[Tzerero]";
			mes "Как тебе нравится жизнь";
			mes "простого Супер-Новичка?";
			mes "Я уверен, ты счастлив" + (Sex?"":"а") + "~";
			next;
			mes "[Tzerero]";
			mes "Расти как Супер-Новичок,";
			mes "помогая обычным ";
			mes "людям и будучи одн" + (Sex?"им":"ой") + " ";
			mes "из них...";
			next;
			mes "[Tzerero]";
			mes "Я призываю тебя";
			mes "развиваться в своей супер-";
			mes "новичковости и быть ";
			mes "исключительным образцом ";
			mes "мирской жизни.";
			close;
		}
		mes "[Tzerero]";
		mes "Здравствуй, я Tzerero,";
		mes "неофициальный глава";
		mes "Великого Общества";
		mes "Новичков.";
		next;
		mes "[Tzerero]";
		mes "Хммм... Ты кажешься мне кем-то большим,";
		mes "чем простым человеком.";
		mes "Да, ты определённо же скорее";
		mes "'необыч" + (Sex?"ен":"на") + "', чем обыч" + (Sex?"ен":"на") + "...";
		next;
		mes "[Tzerero]";
		mes "*Вздох* Полагаю, тебе ";
		mes "никогда не понять ";
		mes "нашего жизненного пути, ";
		mes "тонкой красоты ";
		mes "обыкновенности. Но ";
		mes "ничего страшного.";
		next;
		mes "[Tzerero]";
		mes "Всё-таки... В этом Мире";
		mes "тысячи разных людей,";
		mes "так что я стараюсь принимать";
		mes "наши различия.";
		mes "Надеюсь, ты тоже.";
		close;
	}
}

aldeba_in,216,169,5	script	Esseray#sn	86,{
	if (Class == Job_Super_Novice_E || Class == Job_Super_Baby_E) {
		mes "[Esseray]";
		mes "Да ты силён, брат! Я не сомневался что ты достигнешь таких высот.";
		close;
	}
	if (Class == Job_SuperNovice || Class == Job_Super_Baby) {
		if (checkre(0))
			callfunc "Esseray_Ex";
		mes "[Esseray]";
		mes "Хм? Ты - член нашего";
		mes "Великого Общества Новичков,";
		mes "я прав? Это - лучший клуб во";
		mes "все времена, да?!";
		next;
		mes "[Esseray]";
		mes "Жить мирской жизнью,";
		mes "согласно принципам";
		mes "мистера Киму-Шауна...";
		mes "Быть обычным - это так здорово!";
		next;
		mes "[Esseray]";
		mes "Давай же попытаемся прожить наши жизни";
		mes "настолько нормально, насколько сможем!";
		mes "К твоему сведению, я - ";
		mes "первый член общества,";
		mes "после мистера Tzerero,";
		mes "конечно!";
		close;
	}
	if (Class == Job_Novice && Upper != 1) {
		mes "[Esseray]";
		mes "Ха~ Ты ещё ничего не знаешь о";
		mes "том, что значит быть обычным человеком!";
		mes "Почему бы тебе не вступить в наше Общество";
		mes "Новичков? Это лучший клуб в Мире~";
		next;
		mes "[Esseray]";
		mes "Да, Супер-Новички - лучшие!";
		mes "Ха-ха-ха-ха-ха!";
		close;
	}
	else {
		mes "[Esseray]";
		mes "Ё~! Ты необыч" + (Sex?"ен":"на") + "...";
		mes "Чёрт, даже, возможно,";
		mes "'особенн" + (Sex?"ый":"ая") + ".'";
		mes "Какая незадача! Что ж...";
		mes "Надеюсь, ты позитивно";
		mes "относишься к жизни.";
		close;
	}
}

aldebaran,54,238,5	script	Kafra Employee#sn	117,{
	if (Class == Job_SuperNovice || Class == Job_Super_Baby || Class == Job_Super_Novice_E || Class == Job_Super_Baby_E) {
		mes "[Kafra Employee]";
		if (Sex )
			mes "Здравствуйте, мистер Супер-Новичок~";
		else
			mes "Здравствуйте, мадам Супер-Новичок.";
		mes "Вы, должно быть, озадачены";
		mes "тем, что остальные";
		mes "сотрудницы Кафры не могут";
		mes "сдать вам в аренду телегу.";
		mes "Но не беспокойтесь...";
		next;
		mes "[Kafra Employee]";
		mes "Я здесь для того, чтобы помочь вам";
		mes "получить телегу...";
		next;
		mes "[Kafra Employee]";
		mes "^3355FF*Шёпотом*^000000";
		mes "^555555Вообще-то, это не входит в мои обязанности,";
		mes "потому что это не соответствует ";
		mes "политике компании. Но я";
		mes "очень сочувствую Супер-Новичкам...";
		mes "...поэтому я здесь.^000000";
		next;
		mes "[Kafra Employee]";
		mes "В любом случае, не желаете ли арендовать телегу? Плата за услугу составит 1,900 зен.";
		next;
		if (select("Арендовать телегу.:Отмена.") == 1) {
			mes "[Kafra Employee]";
			mes "Ох, здесь есть одна тонкость...";
			mes "Выучили ли вы умение 'Push Cart'?";
			mes "Я могу дать вам телегу, но";
			mes "если вы не умеете";
			mes "с ней обращаться, вы просто";
			mes "спустите деньги на ветер.";
			next;
			mes "[Kafra Employee]";
			mes "Убедитесь, что у вас есть";
			mes "хотя бы 1 уровень в умении 'Push Cart'.";
			next;
			if (select("Арендовать телегу.:Отмена.") == 1) {
				if (checkcart()) {
					mes "[Kafra Employee]";
					mes "У вас же уже есть телега.";
					close;
				}
				if (Zeny >= 1899) {
					set RESRVPTS, RESRVPTS + 190;
					set zeny,zeny-1900;
					if (getskilllv("MC_PUSHCART") > 0) setcart;
					mes "[Kafra Employee]";
					mes "Спасибо за использование моей услуги.";
					mes "Несмотря на то, что ";
					mes "то, что я делаю, может быть незаконно,";
					mes "я уверена, что это нужно моим клиентам.";
					close;
				}
				else {
					mes "[Kafra Employee]";
					mes "Сожалею, но у вас недостаточно денег. Стоимость услуги составляет 1,900 зен.";
					close;
				}
			}
			mes "[Kafra Employee]";
			mes "Приходите ещё.";
			mes "Спасибо за пользование моими услугами.";
			close;
		}
		mes "[Kafra Employee]";
		mes "Приходите ещё.";
		mes "Спасибо за пользование моими услугами.";
		close;
	}
	mes "[Kafra Employee]";
	mes "Добрый де... Ой!";
	mes "Не думаю, что могу предложить";
	mes "вам какие-либо услуги...";
	mes "Пожалуйста, обратитесь к другой";
	mes "сотруднице Корпорации Кафра. Примите мои";
	mes "извинения за доставленные неудобства...";
	close;
}

//============================================================ 
// Old changelog
//============================================================ 
//= Dialogs From Some Other Scripts (Most Of It)
//= 1.3-1.5 Added Baby class support (and to the Kafta SN Cart
//= renter, too) [Lupus]
//= 1.6 Added a fix which prevent High Novices passing
//= this Job Quests again. [Lupus]
//= 1.7 Fixed Baby Class Support. [Lupus]
//= 1.8 Now a Baby can become a Super Baby [Lupus]
//============================================================ 
