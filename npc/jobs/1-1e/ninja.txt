//===== rAthena Script ======================================= 
//= Ninja Job Quest
//===== By: ================================================== 
//= Legionaire, Kisuka
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Ninja Job Change Quest
//===== Additional Comments: ================================= 
//= 1.1 Optimized, updated, checked [Lupus]
//= 1.2 Added Akagi [Playtester]
//= 1.3 Updated dialogs to official ones and optimized [SinSloth]
//= 1.4 Fixed a minor bug [SinSloth]
//= 1.5 Updated to match AEGIS script and Added Quest Log commands. [Kisuka]
//= 1.6 Fixed AEGIS amatsu warp typo. [Kisuka]
//= 1.7 Kagerou/Oboro support (Class -> BaseClass) [Euphy]
//============================================================ 
//= Translated en -> ru [Akaineko] 
//============================================================

alberta,30,65,3	script	Akagi	730,{
	if (Class == Job_Novice) {
		if (JobLevel == 10) {
			mes "[Акаги]";
			mes "Хммм...";
			mes "Похоже, тебя привело чувство, что здесь тебя что-то ждёт. Скажи, ищещь ли ты пути терпения?";
			next;
			if(select("Нет:Да") == 1) {
				mes "[Акаги]";
				mes "Ясно. Полагаю, каждому своё. Береги себя.";
				close;
			}
			mes "[Акаги]";
			mes "Хорошо. Позволь мне направить тебя к началу этого пути...";
			close2;
			switch(rand(3)) {
				case 1:
					warp "amatsu",170,229;
					end;
				case 2:
					warp "amatsu",216,188;
					end;
				default:
					warp "amatsu",178,176;
					end;
			}
		} else {
			mes "[Акаги]";
			mes "Хмм? Я ничем не смогу тебе помочь, пока не подрастёшь...";
			close;
		}
	} else {
		mes "[Акаги]";
		mes "Хммм...";
		mes "Ты и я...";
		mes "Мы весьма похожи по уровню боевых навыков. Я был бы не прочь с тобой потренироваться как-нибудь.";
		close;
	}
}

que_ng,30,65,3	script	Cougar#nq	730,{
	if (Upper == 2) {
		mes "[Коугар]";
		mes "Я... Никогда не встречал малыша с такой силой! У-убирайся, псих!";
		close;
	}
	if (Class == Job_Novice) {
		if (JobLevel < 10) {
			mes "[Коугар]";
			mes "Хм? Хочешь научиться искусству Ниндзя? Но ты ещё не особенно опыт"+(Sex?"ен":"на")+". Приходи, когда хорошенько научишься побеждать чудовищ.";
			close;
		}
		if (NINJ_Q == 0) {
			mes "["+strcharinfo(0)+"]";
			mes "Простите.";
			mes "Здравствуйте?";
			next;
			mes "[Коугар]";
			mes "...............................";
			mes "Как тебе это удалось?";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Что удалось? Я ничего не делал"+(Sex?"":"а")+", вы же не думаете...";
			next;
			mes "[Коугар]";
			mes "К-как тебе удалось меня увидеть? Я же невидим для обычного глаза! А, так, значит, тебя послал Котяра Джо, чтобы убить меня! Я не попадусь на твои уловки! Умри!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Стойте! Я этого Котяру Джо даже не знаю! Успокойтесь, здесь насилие ни к чему!";
			next;
			mes "[Коугар]";
			mes "Как..?";
			mes "Как тебе удалось избежать моих атак? Ничего себе, талант, это точно.";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "...............................";
			mes "Я приш"+(Sex?"ёл":"ла")+", чтобы стать Ниндзей.";
			next;
			mes "[Коугар]";
			mes "...Ох. И всё? Хмм, у тебя немалый потенциал, но прямо сейчас я ничего не могу сделать. У меня столько врагов, что я должен ни на секунду не терять бдительность.";
			next;
			mes "[Коугар]";
			mes "Этот Котяра Джо просто зверь! Может ударить в любую минуту! И не остановится ни перед чем, чтобы победить врагов!";
			next;
			mes "[Коугар]";
			mes "Погоди-ка, у меня появилась идея. Может, ты сможешь мне помочь. Сделай, что я попрошу, и я поделюсь с тобой тем, что умею.";
			next;
			if(select("Конечно.:Спасибо, не надо.") == 2) {
				mes "[Коугар]";
				mes "Хм? Ну, ладно. Хотя я не вижу причин, почему мы не могли бы оказать друг другу небольшую услугу...";
				close;
			}
			mes "[Коугар]";
			mes "Отлично! В общем, я хотел спросить Котяру Джо, не согласится ли он на временное перемирие. Наши ресурсы истощены, нужно время на подготовку.";
			next;
			mes "[Коугар]";
			mes "Доставь ему в Эйнброх это письмо, пожалуйста. Он мастер маскировки, так что гляди в оба. Ищи его в каком-нибудь высоком месте.";
			next;
			mes "[Коугар]";
			mes "М-да, Котяра Джо собаку съел на прятках на высоте. Короче, доставишь письмо и возвращайся, мне нужно знать, что он ответит.";
			set NINJ_Q,1;
			setquest 6015;
			close;
		}
		else if (NINJ_Q == 1) {
			mes "[Коугар]";
			mes "Задание, конечно, не самое срочное, но поспеши, пожалуйста, в Эйнброх и доставь моё письмо Котяре Джо.";
			close;
		}
		else if (NINJ_Q == 2) {
			mes "[Коугар]";
			mes "Тебе удалось донести письмо до Котяры? Мне очень нужен его ответ, согласен ли он на временное перемирие. Посмотри там, может удастся вызнать чего.";
			close;
		}
		else if (NINJ_Q == 3) {
			mes "[Коугар]";
			mes "О, ты вернул"+(Sex?"ся":"ась")+". Похоже, с ответом от Котяры Джо. Отлично, отлично, дай-ка посмотреть.";
			next;
			mes "[Коугар]";
			mes "Что..?! Как он мог отклонить моё предложение о перемирии?! Это может означать только одно: что он сделал ещё один Кунай. Чёрт! Теперь я мертвец, если не поймаю его раньше!";
			next;
			mes "[Коугар]";
			mes "Слушай, похоже, тебе придётся помочь мне ещё разок! Мне нужно, чтобы ты достал"+(Sex?"":"а")+" кое-какие материалы, из которых я смогу изготовить себе Кунай и встретить Котяру Джо. Тогда я сделаю тебя Ниндзей.";
			next;
			mes "[Коугар]";
			mes "Всё, что тебе нужно мне принести --";
			mes "^3355FF5 "+(getitemname(7053))+"^000000 и";//Cyfar
			mes "^3355FF1 "+(getitemname(1010))+"^000000.";//Phracon
			mes "Не теряй времени, пожалуйста!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Ха? Забавно. Котяра Джо попросил меня принести то же самое.";
			next;
			mes "[Коугар]";
			mes "Проклятье! Это может означать только... Что ты помог"+(Sex?"":"ла")+" Джо сделать Кунай! О, нет! Я должен был предусмотреть это! Но всё равно уже поздно. Поспеши!";
			set NINJ_Q,4;
			changequest 6017,6018;
			close;
		}
		else if (NINJ_Q == 4) {
			if (countitem(7053) < 5 || countitem(1010) < 1) {
				mes "[Коугар]";
				mes "Добудь скорее";
				mes "^3355FF5 "+(getitemname(7053))+"^000000 и";//Cyfar
				mes "^3355FF1 "+(getitemname(1010))+"^000000,";//Phracon
				mes "чтобы я сделал себе Кунай и мог противостоять Котяре Джо!";
				close;
			}
			if (SkillPoint != 0) {
				mes "[Коугар]";
				mes "Ай-яй... У тебя остались свободные очки умений. Используй их перед тем, как сменить профессию, а?";
				close;
			}
			mes "[Коугар]";
			mes "Ага, вот и ты, и материалы прин"+(Sex?"ёс":"есла")+". Раньше, чем я ожидал, а? Отлично, теперь, как я и обещал, я сделаю тебя Ниндзей.";
			next;
			mes "[Коугар]";
			mes "Позволь мне представиться официально. Я Старший Ниндзя Коугар из Организации Ниндзя Тоуга, я лидер поисковой группы, ищущей сэра Казму.";
			next;
			mes "[Коугар]";
			mes "Сэр Казма был вождём моей деревни, но сбежал. Это привело ко внутреннему конфликту в Организации. Ситуация сейчас очень нестабильна...";
			next;
			mes "[Коугар]";
			mes "Из-за этой нестабильности я и не хотел принимать тебя поначалу. Но ты доказал"+(Sex?"":"а")+", что действительно досто"+(Sex?"ин":"йна")+" стать одн"+(Sex?"им":"ой")+" из нас.";
			next;
			mes "[Коугар]";
			mes "Согласно письму, Джо тоже о тебе хорошего мнения. Главное, помни: для тебя как для Ниндзи миссия имеет наивысший приоритет. Но не давай сопутствующим миссии обстоятельствам затмить сознательность.";
			next;
			mes "[Коугар]";
			mes "''Секретность превыше всего.''";
			mes "Чтобы хранить тайны в тени, покупай специальное оружие только у проверенных людей. Помни это.";
			next;
			mes "[Коугар]";
			mes "Сегодня я приветствую тебя как нового члена Организации Ниндзя Тоуга. Будь быстр"+(Sex?"":"а")+", как ветер, тих"+(Sex?"":"а")+", как ниспадающая тень.";
			callfunc "Job_Change",Job_Ninja;
			set NINJ_Q,5;
			delitem 7053,5; // Cyfar
			delitem 1010,1; // Phracon
			getitem 13010,1;	// Asura
			completequest 6018;
			close;
		}
		else {
			mes "[Коугар]";
			mes "Как поживаешь? Не упускай тренировок: тебе нужно уметь исчезнуть, не оставив и следа. Любой Ниндзя будет уважать тебя~";
			close;
		}
	} else {
		if (BaseClass == Job_Ninja) {
			mes "[Коугар]";
			mes "Как поживаешь? Не упускай тренировок: тебе нужно уметь исчезнуть, не оставив и следа. Любой Ниндзя будет уважать тебя~";
			close;
		} else {
			mes "[Коугар]";
			mes "Что..?";
			mes "Как тебе удалось обнаружить меня среди теней?! Ты, наверное, больше чем просто авантюрист, а?";
			close;
		}
	}
}

einbroch,184,194,3	script	Suspicious Man#nq	 	881,{
	if (NINJ_Q == 1) {
		mes "[Подозрительный тип]";
		mes "Я столько разных стран видел, но нигда нет места выше этой башни. В сравнении с ней домики моей родины кажутся такими крошечными...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Ох, не из Амацу ли вы? Я ищу человека по имени Котяра Джо, он тоже оттуда.";
		next;
		mes "[Подозрительный тип]";
		mes "...Нет. Нет, я вообще-то из Излюда, а сюда приехал просто за кое-какими минералами. А зачем тебе этот... Котяра Джо?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Ну, мне нужно доставить ему послание и ответ на него моему нанимателю, и тогда я смогу стать Ниндзей.";
		next;
		mes "[Подозрительный тип]";
		mes "Да? Я тут покопался в памяти и мне кажется, что я встречал его здесь разок. Но он предпочитает, чтобы его звали не ''Котяра Джо'', а ''Рыжий Леопард Джо''.";
		next;
		mes "[Подозрительный тип]";
		mes "Я правда хочу помочь тебе его найти, но сначала мне нужно раздобыть кое-какие минералы. Может, ты мне с этим поможешь? Тогда и я тебе помогу.";
		next;
		if(select("Мне проще само"+(Sex?"му":"й")+".:Конечно, я помогу.") == 1) {
			mes "[Подозрительный тип]";
			mes "Уверен"+(Sex?"":"а")+"..?";
			mes "Рыжий Леопард Джо настоящий мастер маскировки. Тебе понадобится вся возможная  помощь...";
			close;
		}
		mes "[Подозрительный тип]";
		mes "Здорово, рад слышать это. Помоги, пожалуйста, найти";
		mes "^3355FF5 "+(getitemname(7053))+"^000000 и";//Cyfar
		mes "^3355FF1 "+(getitemname(1010))+"^000000.";//Phracon
		set NINJ_Q,2;
		changequest 6015,6016;
		close;
	}
	else if (NINJ_Q == 2) {
		if (countitem(7053) < 5 || countitem(1010) < 1) {
			mes "[Подозрительный тип]";
			mes "Принеси, пожалуйста, поскорее";
			mes "^3355FF5 "+(getitemname(7053))+"^000000 и";//Cyfar
			mes "^3355FF1 "+(getitemname(1010))+"^000000,";//Phracon
			mes "а я помогу тебе найти Рыжего Леопарда Джо.";
			close;
		}
		mes "[Подозрительный тип]";
		mes "Отлично, отлично. Прин"+(Sex?"ёс":"есла")+" минералы... Теперь моя очередь помогать.";
		mes "Давай, что там за письмо.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "?????!!";
		next;
		mes "[Подозрительный тип]";
		mes "Что такое? Коугар ведь передал мне письмо?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Вы...";
		mes "Вы, что, Котяра Джо?";
		next;
		mes "[Подозрительный тип]";
		mes "...Да, хотя я предпочитаю Рыжего Леопарда Джо. Тебя послал Коугар, я прав? Только он зовёт меня котярой. Значит, хочешь стать Ниндзей. *усмехается*";
		next;
		mes "[Рыжий Леопард Джо]";
		mes "Если хочешь быть настоящ"+(Sex?"им":"ей")+" Ниндзей, никогда не теряй бдительности и следи за тем, что ты видишь, что происходит, чему ты веришь. Помни: если твоя тайна раскрыта -- тебе конец.";
		next;
		mes "[Рыжий Леопард Джо]";
		mes "Не пренебрегай тихим перемещением и всегда исчезай без следа. Вот в чём наша сила: в том, что нас не отличить от тени.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Понятно...";
		mes "...........";
		next;
		mes "[Рыжий Леопард Джо]";
		mes "Ну, хватит пока, давай письмо. Ну-ка... Хм. Я-то думал, Коугар снова бросит мне вызов, а он просит о перемирии? Ха!";
		next;
		mes "[Рыжий Леопард Джо]";
		mes "Спасибо тебе, у меня наконец-то есть минералы для создания Куная! Хахаха! Нет, никакого перемирия, когда у меня такое преимущество!";
		next;
		mes "[Рыжий Леопард Джо]";
		mes "Надо написать ему. А ещё я даю тебе свою рекомендацию... Из тебя получится отличный Ниндзя, даже несмотря на то, как я тебя недавно надул. Ха ха ха!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "......";
		mes ".........";
		mes "............";
		next;
		mes "[Рыжий Леопард Джо]";
		mes "Вот, держи. Отнеси это обратно Коугару. До Амацу долгий путь, так что я тебя отправлю попрямее...";
		delitem 1010,1;	// Phracon
		delitem 7053,5;	// Cyfar
		set NINJ_Q,3;
		changequest 6016,6017;
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if (NINJ_Q == 3) {
		mes "[Рыжий Леопард Джо]";
		mes "Э? Не знаю, что у тебя произошло, но мой ответ Коугару, похоже, так и не был доставлен. Закинуть тебя в Амацу?";
		next;
		if(select("Нет, спасибо.:Да, пожалуйста.") == 1) {
			mes "[Рыжий Леопард Джо]";
			mes "Не вопрос. Просто хотел сэкономить твоё время.";
			close;
		}
		mes "[Рыжий Леопард Джо]";
		mes "Хорошо. До встречи.";
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if (NINJ_Q == 4) {
		mes "[Рыжий Леопард Джо]";
		mes "Коугар тоже попросил тебя добыть ему материалы? Не могу сказать, что виню его. Всё равно даже в честном поединке я его уделаю~";
		close;
	}
	else if (NINJ_Q == 5 && BaseClass == Job_Ninja) {
		mes "[Рыжий Леопард Джо]";
		mes "О, ты уже Ниндзя~";
		mes "Тебе предстоят бесчисленные тренировки и интереснейшая жизнь. Главное -- сливайся с тенями.";
		close;
	}
	else {
		mes "[Турист]";
		mes "Я повидал столько разных стран, но нигде нет места выше этой башни. В сравнении с ней домики моей родины кажутся такими крошечными...";
		close;
	}
}
