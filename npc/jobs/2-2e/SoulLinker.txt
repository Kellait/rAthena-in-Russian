//===== rAthena Script ======================================= 
//= Soul Linker Job Quest
//===== By: ================================================== 
//= Celestria & Samuray22
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job quest for Soul Linker class.
//= [Vars Used]
//= SOUL_Q = Job Change Phase. (Max 4)
//===== Additional Comments: ================================= 
//= 1.2 Rescripted to Aegis 10.3 standards. [Samuray22]
//= 1.3 Slight updates and fixes to the script. [L0ne_W0lf]
//=     Removes the use of labels, and corrects other errors.
//=     Removed the GM-use only NPC from the sec_in map. 
//=     No longer used a global var, uses an NPC var instead.
//= 1.3a Deleted Empty Color Tag. (bugreport:1572) [Samuray22]
//= 1.4 Replaced effect numerics with constants. [L0ne_W0lf]
//= 1.5 Added Quest Log commands. [Kisuka]
//= 1.6 Updated to match the latest official script. [Euphy]
//============================================================ 

morocc_in,174,30,6	script	Kid#link1	716,{
	if (Class == Job_Soul_Linker) {
		mes "[Майя]";
		mes "Удачи в твоих странствиях. Чем больше шаманских умений ты изучаешь, тем больше ты сможешь помочь союзникам силами духов...";
		close;
	}
	if (Class == Job_Star_Gladiator) {
		mes "[Дитя]";
		if (Sex == 1) {
			mes "Ты воин Солнца? Мне знакомы";
		}
		else {
			mes "Ты воин Луны? Мне знакомы";
		}
		mes "ваши пути. В конце концов, наши с вами умения произросли из Тхэквондо, верно?";
		close;
	}
	if (Class != Job_Taekwon) {
		mes "[Дитя]";
		mes "Мм? Мне нечего тебе предложить. Но если ты знаешь какого-нибудь опытного Тхэквондиста, ему могут оказаться полезны мои знания.";
		close;
	}
	if (JobLevel < 40) {
		mes "[Дитя]";
		mes "О, ты изучаешь Тхэквондо. Молодец. Продолжай в том же духе.";
		close;
	}
	else if (JobLevel > 39) {
		if (SOUL_Q == 0) {
			mes "[Дитя]";
			mes "...";
			mes "Эй!";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "Ты это мне?";
			next;
			mes "[Дитя]";
			mes "Да, тебе. Подойди, чай не в лесу.";
			next;
			if (select("Ты ужасно грубый ребёнок!:Не обращать внимания.") == 1) {
				mes "[Дитя]";
				mes "Тебе ещё повезло, что ты мне нужн"+(Sex?"":"а")+"! Может, я и выгляжу как дитятко, но мне больше трёх тысяч лет!";
				emotion e_pif;
				next;
				mes "[Дитя]";
				mes "А теперь послушай...";
				mes "Я вижу, что ты ученик Тхэквондо. Это уважаемое искусство, но у меня для тебя есть предложение.";
				emotion e_heh;
				next;
				mes "[Дитя]";
				mes "Я смотрю на тебя и уже могу сказать, что ты весьма чист"+(Sex?"":"а")+" душой. Не хочу, чтобы такой потенциал ушёл не туда. Хочешь стать ''Шаман"+(Sex?"ом":"нкой")+"''?";
				next;
				if (select("Ха! Шути больше~:Шаман"+(Sex?"ом":"кой")+"?") == 1) {
					mes "[Дитя]";
					mes "Ты... Ты мне не веришь? Я ужасно серьёзен. Неужели нельзя отвлечься, что я ребёнок, на какую-то минуту? *Пф* ...Молодёжь.";
					close;
				}
				mes "[Дитя]";
				mes "Шаман взаимодействует с духами павших воинов, всё ещё желающих сражаться в мире живых. Сами по себе они не могут проявляться в нашем мире.";
				next;
				mes "[Дитя]";
				mes "Однако из-за своей духовной чистоты ты привлекаешь духов. При должной тренировке ты сможешь давать силы духов своим союзникам.";
				next;
				mes "[Дитя]";
				mes "Но не себе. И в зависимости от твоих умений как шамана сможешь усиливать носителей определённых профессий.";
				next;
				mes "[Дитя]";
				mes "Стать шаманом означает открыть для себя целый новый мир. Но я знаю, что для тебя это возможно. Что скажешь?";
				next;
				if (select("Нет. По крайней мере, не сейчас...:Хорошо. Что мне делать?") == 1) {
					mes "[Дитя]";
					mes "Ах, ладно. Если всё-таки решишь стать шаманом, приходи ко мне в любое время.";
					close;
				}
				set SOUL_Q,1;
				setquest 6005;
				mes "[Дитя]";
				mes "Так ты соглас"+(Sex?"ен":"на")+" стать шаман"+(Sex?"ом":"кой")+"? Прекрасно!";
				mes "Для начала тебе понадобятся кое-какие предметы. Я потом обьясню, для чего они.";
				next;
				mes "[Дитя]";
				mes "Принеси";
				mes "^0000FF1 "+(getitemname(732))+"^000000,";//Crystal_Jewel__
				mes "^0000FF1 "+(getitemname(929))+"^000000 и";//Immortal_Heart 
				mes "^0000FF1 "+(getitemname(748))+"^000000.";//Witherless_Rose
				mes "И постарайся не заставить меня ждать, хорошо?";
				close;
			}
			mes "[Дитя]";
			mes "Хух..?";
			mes "Стой, ты куда?";
			mes "Эй, я с тобой разговариваю!";
			close;
		}
		else if (SOUL_Q == 1) {
			if (Class == Job_Taekwon) {
				mes "[Дитя]";
				mes "Ты вернул"+(Sex?"ся":"ась")+"?";
				mes "Ну что, прин"+(Sex?"ёс":"есла")+" ";
				mes "^0000FF1 "+(getitemname(732))+"^000000,";
				mes "^0000FF1 "+(getitemname(929))+"^000000 и";
				mes "^0000FF1 "+(getitemname(748))+"^000000,";
				mes "как было нужно?";
				next;
				if (select("Вот они.:Ещё нет...") == 1) {
					if (countitem(732) > 0 && countitem(929) > 0 && countitem(748) > 0) {
						delitem 732,1; //Crystal_Jewel__
						delitem 929,1; //Immortal_Heart
						delitem 748,1; //Witherless_Rose
						set SOUL_Q,2;
						changequest 6005,6006;
						mes "[Дитя]";
						mes "Отлично, тут всё, что нужно. Но прежде, чем мы начнём, позволь представиться. Меня зовут Майя, и я живу здесь уже больше трёх тысяч лет.";
						next;
						mes "[Майя]";
						mes "Если не вдаваться в подробности, меня избрали для миссии находить и инициировать шаманов. Отчасти поэтому смерть меня избегает.";
						next;
						mes "[Майя]";
						mes "Мне сейчас нужно закончить кое-какие приготовления с предметами, которые ты прин"+(Sex?"ёс":"есла")+", так что можешь подойти чуть попозже? Тогда и поговорим.";
						close;
					}
					mes "[Дитя]";
					mes "Мм...?";
					mes "Эй, ты кое-что забыл"+(Sex?"":"а")+". В следующий раз будь внимательнее!";
					emotion e_pif;
					next;
					mes "[Дитя]";
					mes "Знаю, только что говорил, но на всякий случай повторю ещё раз:";
					mes "^0000FF1 "+(getitemname(732))+"^000000,";
					mes "^0000FF1 "+(getitemname(929))+"^000000 и";
					mes "^0000FF1 "+(getitemname(748))+"^000000.";
					close;
				}
				mes "[Дитя]";
				mes "Мм. Ничего страшного. Мне некуда спешить, у меня целая вечность времени, хотя я и не люблю тратить его впустую.";
				close;
			}
			set SOUL_Q,0;
			mes "[Дитя]";
			mes "Уш"+(Sex?"ёл":"ла")+" в воины Солнца, Луны и Звёзд? Эх, а такой талант был. Ну, я тебя не виню...";
			close;
		}
		else if (SOUL_Q == 2) {
			if (SkillPoint) {
				mes "[Майя]";
				mes "У тебя остались нераспределённые очки умений. Выучись до конца и возвращайся.";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[Майя]";
				mes "Сейчас другой будущий шаман проходит церемонию. Подожди, пока он закончит, и мы приступим, хорошо?";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[Майя]";
			mes "Отлично, с приготовлениями покончено. Сейчас мы перейдём к ритуалу и ты станешь шаман"+(Sex?"ом":"кой")+". Закрой глаза...";
			close2;
			warp "job_soul",30,30;
			end;
		}
		else if (SOUL_Q > 2) {
			mes "[Майя]";
			mes "Готов"+(Sex?"":"а")+" ли ты погрузиться в глубины своего разума ещё раз?";
			next;
			if (select("Нет:Да") == 1) {
				mes "[Майя]";
				mes "Хорошо, возвращайся по готовности, я буду ждать тебя здесь.";
				close;
			}
			if (.SoulLinkerTest == 1) {
				mes "[Майя]";
				mes "Сейчас другой будущий шаман проходит церемонию. Подожди, пока он закончит, и мы приступим, хорошо?";
				close;
			}
			donpcevent "Timer#link3::OnEnable";
			set .SoulLinkerTest,1;
			mes "[Майя]";
			mes "Хорошо. Закрой глаза и расслабься. Мы вернёмся в глубины твоего разума.";
			close2;
			warp "job_soul",30,30;
			end;
		}
	}

OnInit:
	set .SoulLinkerTest,0;
	end;
}

job_soul,30,31,0	script	Maia#link2::SLTester	-1,3,3,{
OnTouch:
	if (Class == Job_Taekwon) {
		if (JobLevel < 40) {
			set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
			mes "[Майя]";
			mes "Хм? Как ты сюда попал"+(Sex?"":"а")+"? Ты не готов"+(Sex?"":"а")+" для риуала. Пойдём я проведу тебя обратно в Моррок...";
			close2;
			warp "morocc",157,47;
			end;
		}
		if (SOUL_Q == 2) {
			mes "[Майя]";
			mes "Узнаёшь ли ты это место? Сейчас мы внутри твоего разума. Духи павших воинов парят здесь в ожидании воплощения своих сил.";
			next;
			mes "[Майя]";
			mes "Немногих из них ты можешь видеть сейчас, остальные придут с тренировкой.";
			next;
			set SOUL_Q,3;
			changequest 6006,6007;
			mes "[Майя]";
			mes "Мы можем находиться здесь три минуты. Полагаю, ты захочешь пообщаться с духами, пока у тебя есть такая возможность.";
			close;
		}
		else if (SOUL_Q == 3) {
			mes "[Майя]";
			mes "Прислушайся к тому, что скажут тебе духи. Они не просто так задержались в этом мире, этому есть причины.";
			close;
		}
		else if (SOUL_Q == 4) {
			mes "[Майя]";
			mes "Теперь ты готов"+(Sex?"":"а")+" стать шаманом. Но если хочешь, можешь продолжить разговаривать с духами.";
			next;
			if (select("Ещё пообщаться с духами:Стать шаманом") == 1) {
				mes "[Майя]";
				mes "Хорошо. Но торопись, время пребывания здесь у нас ограничено. Хотя если будет необходимо, придётся вернуться...";
				close;
			}
			if (ismounting()) {
				mes "[Майя]";
				mes "Пока ты верхом, я не могу изменить тебя! Слезь с ездового животного.";
				close;
			}
			mes "[Майя]";
			mes "Тогда начнём ритуал.";
			mes "Эти предметы послужат для того, чтобы сделать тебя носителем силы павших воинов и проводником её в битвах.";
			next;
			mes "[Майя]";
			mes "Эта неувядающая роза";
			mes "увянет вместо тебя...";
			specialeffect EF_MAPPILLAR2,AREA,"Maia#link2";
			next;
			mes "[Майя]";
			mes "Эта неувядающая роза";
			mes "увянет вместо тебя...";
			mes "Это бессмертное сердце";
			mes "перестанет биться вместо твоего.";
			next;
			mes "[Майя]";
			mes "Эта неувядающая роза";
			mes "увянет вместо тебя...";
			mes "Это бессмертное сердце";
			mes "перестанет биться вместо твоего.";
			mes "Этот алмаз обратится в прах";
			mes "вместо твоего смертного тела.";
			next;
			mes "[Майя]";
			mes "Павшие, но не сложившие оружия... Будут сражаться за тебя!";
			mes "Используй свои силы мудро и в правильных целях.";
			next;
			if (SkillPoint) {
				mes "^0000ffУ тебя ещё остались непотраченные очки умений. Выучись до конца и возвращайся!^000000";
				close;
			}
			completequest 6008;
			callfunc "Job_Change",Job_Soul_Linker;
			callfunc "F_ClearJobVar";	// clears all job variables for the current player
			set SOUL_Q,0;
			mes "[Майя]";
			mes "Желаю тебе всего самого лучшего в новой жизни. Окружай себя союзниками, и духи смогут защищать тебя и помогать в битвах. До свидания, друг.";
			close2;
			set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
			donpcevent "Timer#link3::OnDisable";
			warp "morocc",157,47;
			end;
		}
		set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
		mes "[Майя]";
		mes "Хмм...?";
		mes "Твоё время пребывать здесь ещё не пришло. Вернёмся в Моррок...";
		close2;
		warp "morocc",157,47;
		end;
	}
	set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	if (Class == Job_Soul_Linker) {
		mes "[Майя]";
		mes "Пришло время тебе вернуться в Мир! В идущей там битве со злом чем больше шаманов - тем лучше...";
	}
	else {
		mes "[Майя]";
		mes "Как странно... Тебе не следует находиться здесь. Я выведу тебя в Моррок...";
	}
	close2;
	warp "morocc",157,47;
	end;
}

job_soul,35,30,6	duplicate(SLTester)	Maia#link6	716

job_soul,30,35,6	script	Monk Spirit#link4	827,{
	if (SOUL_Q == 2) {
		mes "[Дух монаха]";
		mes "Кто я..?";
		mes "Я думаю... Я думаю, будет лучше, если ты сначала поговоришь с Майей. Кто я и что я - вопрос сложный...";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Дух монаха]";
		mes "При жизни учителя не жалели сил и передавали мне всё, что знали сами. Но... Я никогда не был удовлетворён уровнем своего мастерства.";
		next;
		mes "[Дух монаха]";
		mes "В смерти я испытал множество страданий, не имея никакой возможности донести свои умения до будущих монахов. Дать свою силу другим - вот единственное, что мне осталось.";
		next;
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		mes "[Дух монаха]";
		mes "Прошу тебя...";
		mes "Помоги мне полностью реализовать потенциал сегодняшних монахов.";
		close;
	}
	mes "[Дух монаха]";
	mes "...";
	close;
}

job_soul,30,25,7	script	Sage Spirit#link5	754,{
	if (SOUL_Q == 2) {
		mes "[Дух мудреца]";
		mes "Поговори с Майей. Боюсь, я могу смутить тебя, если Майя не обьяснит тебе ситуацию...";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Дух мудреца]";
		mes "Жажда знаний привела меня к невероятной силе: при жизни я мог разрушить всё, что захочу. Немногие коллеги могли достичь моего уровня...";
		next;
		mes "[Дух мудреца]";
		mes "Я умер, но оказался неспособен перейти в следующий мир. Я всё ещё жажду использовать свои знания: строить и уничтожать.";
		next;
		mes "[Дух мудреца]";
		mes "Я буду рад дать всю свою силу коллеге, который сможет ей воспользоваться. Но для этого мне необходима твоя помощь. Прошу, позволь мне быть твоим духом-союзником.";
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		next;
		mes "[Дух мудреца]";
		mes "Ты единственн"+(Sex?"ый":"ая")+", кто способен принести покой моей страждущей душе...";
		close;
	}
	mes "[Дух мудреца]";
	mes "...";
	close;
}

job_soul,25,30,5	script	Alchemist Spirit#link7	744,{
	if (SOUL_Q == 2) {
		mes "[Дух алхимика]";
		mes "О! Я очень хочу поговорить с тобой, но мои слова будут пусты, пока ты не выслушаешь Майю. Но да, мне нужна твоя помощь.";
		close;
	}
	else if (SOUL_Q > 2) {
		mes "[Дух алхимика]";
		mes "Без преувеличения, я была самым быстрым алхимиком своего времени. Может, даже самым быстрым алхимиком в истории. Но потом меня занесло, и я убилась по неосторожности.";
		next;
		mes "[Дух алхимика]";
		mes "Но смерть не избавила меня от умений. На самом деле я даже улучшила их, умерев. Но я не могу покинуть этот мир, не передав своих навыков...";
		set SOUL_Q,4;
		if(checkquest(6008) == -1) {
			changequest 6007,6008;
		}
		next;
		mes "[Дух алхимика]";
		mes "Как дух я бессильна, но с твоей помощью я могла бы влиять на современных алхимиков и помогать им совершенствоваться. Умоляю, дай мне такую возможность...";
		close;
	}
	mes "[Дух алхимика]";
	mes "...";
	close;
}

job_soul,1,5,0	script	Timer#link3	111,{
	end;

OnEnable:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	end;

OnTimer60000:
OnTimer120000:
	if (getmapusers("job_soul") == 0) {
		stopnpctimer;
		set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	}
	end;

OnTimer180000:
OnTimer181000:
OnTimer182000:
	mapwarp "job_soul","morocc",157,47;
	end;

OnTimer183000:
	mapwarp "job_soul","morocc",157,47;
	set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
	stopnpctimer;
}

sec_in02,35,153,0	script	Soul Linker Var	871,{
	callfunc "F_GM_NPC";
	mes "[Soul Linker Var]";
	mes "I can reset the Soul Linker";
	mes "NPCs if a Soul Linker candidate";
	mes "encounters a problem during the";
	mes "end of the job quest. Please do";
	mes "not use this function if players are still in the Quest Map.";
	next;
	if (callfunc("F_GM_NPC",1854,0) < 1) {
		mes "[Soul Linker Var]";
		mes "Password";
		mes "is incorrect.";
		close;
	} else {
		mes "[Soul Linker Var]";
		mes "Would you like to";
		mes "reset the Soul Linker";
		mes "Global Variable?";
		next;
		switch(select("Reset:Cancel")) {
		case 1:
			mes "[Soul Linker Var]";
			mes "The Soul Linker";
			mes "Job Quest NPCs";
			mes "have been reset.";
			set getvariableofnpc(.SoulLinkerTest,"Kid#link1"),0;
			close;
		case 2:
			mes "[Soul Linker Var]";
			mes "You have canceled";
			mes "this command.";
			close;
		}
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= A temp Soul Linker Job Changer based on the kRO quest.
//= Quest info from RagnaInfo. Sprites from kRO screenshots
//= 1.0 Optimized and updated [Lupus]
//= 1.1 Fixed NPC names according to iRO [Lupus]
//============================================================ 
