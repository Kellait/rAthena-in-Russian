//===== rAthena Script ======================================= 
//= Cool Event Corp. Staff
//===== By: ================================================== 
//= Gepard
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Cool Event Corp. Staff (Save, Storage & Pushcart Service)
//= in Arunafeltz Region.
//===== Additional Comments: ================================= 
//= 1.0 First version, iRO Sakray X.4 [Gepard]
//= 1.1 Optimized, fixed a conditional bug [Lupus]
//= 1.1a Redo back. It wasn't a bug [Lupus]
//= 1.2 Added Rachel Cool Event Corp. Staff [Playtester]
//= 1.3 Added cutins, closed Kafra Password exploit
//=     until we got a client/packet based password support [Lupus]
//= 1.4 Added Veins Cool Event Corp. Staff. [L0ne_W0lf]
//= 1.5 Cool Event Corp staff is now more iRO like. [L0ne_W0lf]
//= 1.6 Wrote a basic function and added VIP features. [Euphy]
//============================================================ 
//= Translated EN -> RU [Akaineko]
//============================================================

//callfunc "F_CoolEventCorp","<menu>","<location>","<save map>",<x>,<y>,"<warp map>";
function	script	F_CoolEventCorp	{
	cutin "zonda_01",2;
	mes "[Сотрудник Компании ''За горизонт'']";
	mes "Добро пожаловать в ";
	mes "Компанию ''За горизонт''. ";
	mes "Наши сотрудники работают, ";
	mes "чтобы превосходить ";
	mes "ваши ожидания. Чем я могу ";
	mes "вам помочь?";
	next;
	switch(select(getarg(0))) {
	case 1:
		mes "[Сотрудник Компании ''За горизонт'']";
		mes "Ваша точка сохранения ";
		mes "зарегистрирована";
		mes getarg(1)+".";
		mes "Спасибо за использование ";
		mes "наших услуг~";
		savepoint getarg(2),getarg(3),getarg(4);
		close2;
		break;
	case 2:
		mes "[Сотрудник Компании ''За горизонт'']";
		if(basicskillcheck() && getskilllv("NV_BASIC") < 6){
			mes "Извините, но, чтобы пользоваться ";
			mes "складом, вам нужно обладать ";
			mes "Базовым навыком хотя бы шестого ";
			mes "уровня.";
			close2;
			break;
		}
		if(Zeny < 40){
			mes "Извините, у вас недостаточно денег. ";
			mes "Доступ в хранилище стоит 40 зен.";
			close2;
			break;
		}
		set Zeny, Zeny - 40;
		mes "Позвольте открыть ваш склад. ";
		mes "Благодарим за то, что вы ";
		mes "поддерживаете Компанию, ";
		mes "пользуясь нашими услугами. ";
		mes "Всего хорошего~";
		callfunc("F_CheckKafCode");	//check your storage password, if set
		close2;
		openstorage;
		break;
	case 3:
		mes "Пожалуйста, выберите ";
		mes "пункт назначения.";
		next;
		if (VIP_SCRIPT && !vip_status(1))
			set .@cost,4400;
		else
			set .@cost,2200;
		if (select(getarg(5)+" -> "+.@cost+" з:Отмена") == 1) {
			if ((countitem(7060) != 0) || (Zeny >= .@cost)) {
				if (countitem(7060) != 0) {
					delitem 7060,1; //Warp_Free_Ticket
				}
				else {
					set Zeny,Zeny-.@cost;
				}
				if (getarg(5) == "Veins") warp "veins",205,101;
				else if (getarg(5) == "Rachel") warp "rachel",115,125;
				cutin "",255;
				end;
			}
			else {
				mes "[Сотрудник Компании ''За горизонт'']";
				mes "Извините, у вас недостаточно ";
				mes "средств. Телепортация в ";
				mes "указанный пункт назначения ";
				mes "стоит "+.@cost+" зен.";
			}
		}
		close2;
		break;
	case 4:
		mes "[Сотрудник Компании ''За горизонт'']";
		if(BaseClass != Job_Merchant){
			mes "Извините, но аренда телеги";
			if (checkre(0))
				mes "доступна только торговцам ";
				mes "и их коллегам.";
			else {
				mes "доступна только торговцам, ";
				mes "кузнецам, алхимикам и их ";
				mes "перерождённым коллегам.";
			}
			close2;
			break;
		}
		if(getskilllv("MC_PUSHCART") == 0){
			mes "Вы сможете арендовать телегу, ";
			mes "только изучив управление ею, ";
			mes "умение / ' Pushcart ' /.";
			close2;
			break;
		}
		if(checkcart() == 1){
			mes "У вас уже есть телега. К ";
			mes "сожалению, мы можем ";
			mes "предоставить только одну ";
			mes "телегу на одного клиента.";
			close2;
			break;
		}
		if(countitem(7061)){
			delitem 7061,1; //Cart_Free_Ticket
			setcart;
			close2;
			break;
		}
		mes "Аренда стоит 800 зен. ";
		mes "Хотите арендовать телегу?";
		next;
		if(select("Взять телегу:Отмена") == 1){
			if(Zeny < 800){
				mes "[Сотрудник Компании ''За горизонт'']";
				mes "Извините, но у вас ";
				mes "недостаточно средств. ";
				mes "Плата за аренду телеги";
				mes "составляет 800 зен.";
			} else {
				set Zeny, Zeny - 800;
				setcart;
			}
		}
		close2;
		break;
	case 5:
		callfunc("F_SetKafCode","[Сотрудник Компании ''За горизонт'']","Cool Event Corp.");
		//the function closes msg and removes cutins
	case 6:
	default:
		mes "[Сотрудник Компании ''За горизонт'']";
		mes "Компания ''За горизонт'' работает";
		mes "над улучшением своего обслуживания.";
		mes "Помогите нам стать лучше путём";
		mes "отзывов и комментариев. Спасибо.";
		close2;
	}
	cutin "", 255;
	end;
}

hugel,88,168,4	script	Cool Event Corp. Staff::CoolEventCorpStaffHugel	874,{
	callfunc "F_CoolEventCorp",
		"Сохраниться:Использовать склад:Взять телегу:Пароль к складу:Отмена",
		"в деревне Хугель","hugel",96,155;
	end;
}

rachel,109,138,5	script	Cool Event Corp. Staff::CoolEventCorpStaffRachel	874,{
	callfunc "F_CoolEventCorp",
		"Сохраниться:Использовать склад:Телепортироваться:Взять телегу:Пароль на склад:Отменить",
		"in the town of Rachel","rachel",113,137,"Veins";
	end;
}

veins,208,128,5	script	Cool Event Corp. Staff::CoolEventCorpStaffVeins	874,{
	callfunc "F_CoolEventCorp",
		"Сохраниться:Использовать склад:Телепортироваться:Взять телегу:Пароль на склад:Отменить",
		"в городе Вейнс","veins",204,103,"Rachel";
	end;
}
