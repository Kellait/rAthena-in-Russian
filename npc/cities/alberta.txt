//===== rAthena Script ======================================= 
//= Alberta Town
//===== By: ================================================== 
//= DZeroX
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Town-specific Alberta NPCs
//===== Additional Comments: ================================= 
//= 1.0 Converted from Aegis 10.4 [DZeroX]
//= 1.1 Optimized, missing next, REMOVED .GATs! again [Lupus]
//= 1.2 Implemented progression fixes provided by $ephiroth. [L0ne_W0lf]
//= 1.2b More pregression clean up provided by Barron-Monster. [L0ne_W0lf]
//= 1.3 Removed npc "Tourist#al" as it's a quest NPC. [L0ne_W0lf]
//= 1.4 Removed "Fastidious Old Man" duplicate. [L0ne_W0lf]
//= 1.5 Updated to match AEGIS script. [Kisuka]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//= 1.7 Bug fixes and syntax. [Capuche]
//===== Translated into Russian: =============================
//= 0.1 Новый скрипт, полный перевод [mad_max] (MistiQ Group © 2007)
//= Коррекция [Vorp]
//= Коррекцияперевода [mad_max] (MistiQ Group © 2008)
//= Актуализация Fenrir
//============================================================ 

alberta,97,51,0	script	Фабиан	84,{
	mes "[Фабиан]";
	mes "Человек... Путешествуя по всему";
	mes "миру, вы можете услышать множество";
	mes "сумашедших вещей.";
	next;
	mes "[Фабиан]";
	mes "Однажды, я услышал о картах";
	mes "обладающих силой монстров.";
	mes "Если случится, что кому-то в руки";
	mes "попадет такая карта,";
	mes "то он обретет силу монстра.";
	next;
	mes "[Фабиан]";
	mes "Мне кажется, что это какая-то";
	mes "причуда или жульничество.";
	mes "Так они хотят заставить собрать нас все карты.";
	mes "Я хочу сказать, разве карта";
	mes "может дать силу монстра?!";
	next;
	mes "[Фабиан]";
	mes "Ну правда...";
	close;
}

alberta,53,39,0	script	Штайнер	100,{
	mes "[Штайнер]";
	mes "Ооо...!";
	mes "Добро пожаловать в Альберту (Alberta),";
	mes "молодой путешественник!";
	next;
	mes "[Штайнер]";
	mes "Простите меня если отвлекаю.";
	mes "Я склоняюсь к тому, что бы";
	mes "провернуть один план.";
	mes "Видите ли, я слышал, что в Геффене (Geffen)";
	mes "есть магазин продающий броню";
	mes "способную защитить от магии.";
	next;
	mes "[Штайнер]";
	mes "Если я куплю побольше оптом,";
	mes "а потом перепродам ее здесь";
	mes "за более высокую цену...";
	close;
}

alberta_in,20,183,0	script	Чад	1_M_03,{
	mes "[Чад]";
	mes "Люди говорят, что легендарное оружие";
	mes "Gungnir никогда не промахивается по своей цели.";
	mes "Я очень буду рад если это правда...";
	next;
	mes "[Чад]";
	mes "Так же люди говорят, что младенцы,";
	mes "которых приносят аисты, ";
	mes "выращивает как побеги";
	mes "Санта Клаус (Santa Claus).";
	mes "Но только в Люти (Lutie).";
	next;
	mes "[Чад]";
	mes "Хотел бы я знать...";
	mes "Хоть что-то";
	mes "из этого";
	mes "правда...";
	close;
}

alberta,131,139,2	script	Пьяный старик	1_M_JOBGUIDER,{
	mes "[Дигл]";
	mes "*^CCCCCCПпривет!^000000*";
	mes "Ч-Что-то ищите?";
	mes "Заблудились!!";
	next;
	switch(select("Не сказать ничего.","Покинуть его.")) {
		case 1:
			mes "[Дигл]";
			mes "Хахахахаха *^666666Ик^000000*... Похоже ты раздражен. Я выгляжу никчемным, но когда-то я был моряком на судне 'Going Mary.'";
			next;
			switch(select("Никогда не слышал о нем.","Правда? Без шуток!")) {
				case 1:
					mes "[Дигл]";
					mes "Никогда не слышал?! Да каждый знает этот пиратский корабль 'Going Mary!' *^666666Ик~^000000*";
					next;
					mes "[Дигл]";
					mes "Эх~ Былые дни. Если б только... если б мы не попали в этот ужасный ШТОРМ... *^666666Ик^000000*";
					next;
					mes "[Дигл]";
					mes "Эх~ Капитан. Я скучаю по нему больше, чем по кому-нибудь еще... Никто не выживал после его меча.";
					close;
				case 2:
					mes "[Дигл]";
					mes "Именно! НИКТО не сравнится с командой 'Going Mary!' И никто не победит капитана в схватке на мечах!";
					next;
					mes "[Дигл]";
					mes "КАПИТАН~!!! *^666666Ик~^000000* Он хотел плавать только со своим мечем и только с НИМ!!";
					next;
					mes "[Дигл]";
					mes "В сражениях все друзья капитана, находящиеся рядом были окружены пламенем!";
					next;
					mes "[Дигл]";
					mes "Похоже этот меч имел таинственную силу, или же это просто капитан был отличным парнем...!";
					next;
					mes "[Дигл]";
					mes "Эээ~~ *^666666Sob^000000* *^666666черт...^000000* Боже, я скучаю по всем им! Я впадаю в депрессию! Пожалуйста, уходите.";
					close;
			}
		case 2:
			mes "[Дигл]";
			mes "И правильно!";
			mes "Убирайтесь прочь~";
			close;
	}
}

alberta,58,80,2	script	Шакир	99,{
	mes "[Шакир]";
	if (rand(2)) {
		mes "У нас, Торговцев, есть свои навыки продажи товаров. Они позволяют нам получать больше денег при продаже вещей.";
		next;
		mes "[Шакир]";
		mes "И это не просто просьбы 'Вы должны дать больше за это, пожалуйста!' Для этого нужно обладать харизмой, и мастером риторики!";
		next;
		mes "[Шакир]";
		mes "С помощью этого невероятного умения мы можем получить на 24% больше зеней. Помни, тренируся больше, что бы совершенствовать его!";
	} else {
		mes "Мы, Торговцы, можем открывать магазины у дорог для торговли.";
		next;
		mes "[Шакир]";
		mes "С помощью умения Скидка (Discount) , мы можем покупать товары в магазинах дешевле, а потом продавать их с телег, которые берем в аренду.";
		next;
		mes "[Шакир]";
		mes "Так же мы можем и путешествовать продавая товары для получения прибыли!";
		next;
		mes "[Шакир]";
		mes "Такой путь более удобен и безопасен. Главное не падать уснув, хотя это может случиться.";
	}
	close;
}

alberta,62,156,2	script	Соня	102,{
	mes "[Соня]";
	switch(rand(3)){
	case 0:
		mes "Эй, однажды прогуливаясь по лесу я увидела маленький зеленый побег который зашевелился.";
		next;
		mes "[Соня]";
		mes "Я захотела посмотреть, что это. И когда приблизилась к побегу и протянула руку, оно вдруг ударило меня!";
		next;
		mes "[Соня]";
		mes "Оно бросилось на меня, поэтому я отпрыгнула назад. И тогда я поняла что это не растение а маленькое животное.";
		next;
		mes "[Соня]";
		mes "Мне повезло, что я не тронула его. Даже такое маленькое животное может быть очень опасно.";
		close;
	case 1:
		mes "Вы знаете этих глупых медведей, которые живут по пути в город Пайон?";
		next;
		mes "[Соня]";
		mes "Шутки ради, я бросила в него камень и он вдруг бросился на меня! Я таааак успугалась, и побежала прочь, и вдруг БАМ!!!";
		next;
		mes "[Соня]";
		mes "Он ударяется о низкую ветку дерева и разносит ее в щепки! Клянусь, больше никогда не буду провоцировать животных ради забавы!";
		close;
	case 2:
		mes "Однажды я видела как стая волков задрала одного из этих глупых медведей!";
		next;
		mes "[Соня]";
		mes "Волки более общны, чем могут казаться на первый взгляд. Если один из них подвергнется нападению, то все волки находящиеся рядом придут к нему на помощь.";
		next;
		mes "[Соня]";
		mes "Я бы дважды подумала, прежде чем напасть на кого-либо из их вида в округе. Будьте осторожны и не подвергайте себя опасности!";
		close;
	}
}

alberta,93,174,2	script	Бабушка Альма	103,{
	mes "[Бабушка Альма]";
	mes "Некоторое время назад, брошенный корабль придрейфовал в гавань Альберты.";
	next;
	mes "[Бабушка Альма]";
	mes "Надеясь спасти оставшихся в живых, некоторые горожане отважились взайти на корабль. И в ужасном испуге они бежали с него, говоря что мертвые ходят внутри корабля.";
	next;
	mes "[Бабушка Альма]";
	mes "Кроме того, корабль просто кишил опасными морскими организмами, и они не могли пробраться внутрь, даже если сильно этого хотели.";
	next;
	mes "[Бабушка Альма]";
	mes "Мы ничего не смогли сделать с этим зловещим судном, и просто оставили его как есть. В настоящее время исследовательские группы пытаются проникать на корабль и уничтожать его монстров.";
	next;
	mes "[Бабушка Альма]";
	mes "Так что, это может быть хорошим опытом для такого новичка как ты, если пойдешь в рекруты. Но если ты недостаточно силен, то не стоит рисковать своей жизнью.";
	close;
}

alberta,189,151,5	script	Фиск	4W_SAILOR,{
	mes "[Фиск]";
	mes "Мы скоро отчаливаем, куда тебе нужно отправиться?";
	next;
	switch(select("Sunken Ship -> 250 zeny.","Izlude Marina -> 500 zeny.","Никуда.")) {
		case 1:
			if (Zeny < 250) {
				mes "[Фиск]";
				mes "Эй не пытайся меня обмануть, я сказал 250 зен!";
				close;
			}
			set Zeny,Zeny - 250;
			warp "alb2trea",43,53;
			end;
		case 2:
			if (Zeny < 500) {
				mes "[Фиск]";
				mes "Врятли у тебя получиться попасть туда без 500 зеней!";
				close;
			}
			set Zeny,Zeny - 500;
			if(checkre(0))
				warp "izlude",195,212;
			else
				warp "izlude",176,182;
			end;
		case 3:
			mes "[Фиск]";
			mes "Хорошо...";
			mes "Нищеброд.";
			close;
	}
}

alb2trea,39,50,6	script	Фиск#a2t	4W_SAILOR,{
	mes "[Фиск]";
	mes "Хотите вернуться на материк в Альберту (Alberta)?";
	next;
	if (select("Да пожалуйста.","Я передумал.")==1)
		warp "alberta",192,169;
	close;
}

alberta,195,151,2	script	Пол	4_M_04,{
	mes "[Пол]";
	mes "Добрый день~";
	mes "Хотите присоединиться к исследовательской группе по Затонувшему Кораблю?";
	next;
	mes "[Пол]";
	mes "Да! Перед тем как начнете, я должен предостеречь вас. Если вы не достаточно сильны, то вам не обязательно туда идти.";
	next;
	mes "[Пол]";
	mes "Ну так что, хотите присоединиться? Вступительный взнос 200 зеней.";
	next;
	switch(select("Присоединиться!","Нет, спасибо.")) {
		case 1:
			if (Zeny < 200) {
				mes "[Пол]";
				mes "Друг мой, кажется у вас недостаточно, денег. Но возвращайтесь назад когда сможете заплатить.";
				close;
			} else {
				set Zeny,Zeny - 200;
				warp "alb2trea",62,69;
				close;
			}
		case 2:
			mes "[Пол]";
			mes "Ну хорошо... я буду рядом, если вдруг вы передумаете.";
			close;
	}
}

alberta,190,173,4	script	Phelix	4_M_03,{
	mes "[Phelix]";
	if ((MaxWeight - Weight) < 10000) {
		mes "Wait a moment!!";
		mes "You have brought too many things!";
		mes "You cannot accept any more items!";
		mes "Please reduce the amount of items,";
		mes "then come see me again.";
		close;
	}
	if (@event_zelopy == 0) {
		mes "The hell are you doing here?";
		mes "There is nothing you can get for free on this ship, if you want somethin', work for it!!";
		next;
		mes "[Phelix]";
		mes "Hmm, so why don't you bring me 10 jellopies and I will give 1 potion. How's that sound?";
		mes "Or if that's too hard for your pansy ass, 3 jellopies for 1 Carrot.";
		next;
		mes "[Phelix]";
		mes "If you're interested in my offer, get me the stuff I mentioned.";
		@event_zelopy = 1;
		close;
	}
	mes "Hmm.. you want to exchange jellopies for Red Potions or some Carrots eh? Well.. which one?";
	next;
	switch(select("Red Potions please.","Carrots please.")) {
	case 1:
		mes "[Phelix]";
		mes "Alright...";
		mes "Let's see";
		mes "what'cha got...";
		next;
		mes "[Phelix]";
		if (countitem(909) < 10) {
			mes "Hey! Weren't you listening?. I said 10 jellopies for 1 Red Potion.. are ya deaf?";
			close;
		} else {
			.@max = countitem(909)/10;
			mes "Hmm, not bad...";
			mes "How many potions";
			mes "do you want to get?";
			next;
			switch(select("As many as I can, please.","I want this many.","Never mind, I like my jellopy.")) {
			case 1:
				delitem 909,.@max*10;// Jellopy
				getitem 501,.@max;// Red_Potion
				break;
			case 2:
				mes "[Phelix]";
				mes "I'm not giving you more then 100 at a time so don't bother, OK? If you don't want any, just say '0'.";
				mes "Right now, the most you can get is " + .@max + " but remember, 100 at most, you want to break my back?.";
				input .@amount;
				next;
				mes "[Phelix]";
				if (.@amount <= 0) {
					mes "Much obliged, come again anytime.";
					close;
				}
				if (.@amount > 100) {
					mes "Hey, what'd I say? 100 at a time at most, you're trying to kill me aren't you!";
					close;
				}
				if (countitem(909) < .@amount*10) {
					mes "Hmm, it looks like you don't have enough. Go get more jellopies if you want anything else from me.";
					close;
				}
				delitem 909,.@amount*10;// Jellopy
				getitem 501,.@amount;// Red_Potion
				break;
			case 3:
				mes "[Phelix]";
				mes "No problem,";
				mes "see you next time.";
				close;
			}
			mes "[Phelix]";
			mes "There you go! As I promised. Don't go suckin' them all down at once.";
			close;
		}
	case 2:
		mes "[Phelix]";
		mes "Alright, let's see what ya got...";
		next;
		mes "[Phelix]";
		if (countitem(909) < 3) {
			mes "Hmm, look pansy ass, I said 3 jellopies for 1 Carrot.. got it?";
			close;
		}
		.@max = countitem(909)/3;
		mes "Not too bad pansy...";
		mes "How many do you want?";
		next;
		switch(select("As many as I can get, please","I want this many.","Never mind, I like my jellopy.")) {
		case 1:
			delitem 909,.@max*3;// Jellopy
			getitem 515,.@max;// Carrot
			break;
		case 2:
			mes "[Phelix]";
			mes "Right I'm not giving you more then 100 at a time so don't bother, okay? If you don't want any, just say '0'.";
			input .@amount;
			next;
			mes "[Phelix]";
			if (.@amount == 0) {
				mes "Alright then, see you next time.";
				close;
			}
			if (.@amount > 100) {
				mes "Hey pansy ass, I said 100 at most, no more then that! I'm not going to break my back for the likes of you!";
				close;
			}
			if (countitem(909) < .@amount*3) {
				mes "Seems you don't have enough. Go get some more if you want anything else.";
				close;
			}
			delitem 909,.@amount*3;// Jellopy
			getitem 515,.@amount;// Carrot
			break;
		case 3:
			mes "[Phelix]";
			mes "Catch'ya later.";
			close;
		}
		mes "[Phelix]";
		mes "There you go~! As I promised. Try not to stuff yer face.";
		close;
	}
}
