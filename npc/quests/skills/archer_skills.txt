//===== rAthena Script =======================================
//= Archer Skill Quest
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.7
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Quest for skills: Arrow Crafting, Arrow Repel
//===== Additional Comments: ================================= 
//= 1.5 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//= 1.6 Updated to latest available official file.
//=     also fixed Item issue [bugreport:5607]. [Masao]
//= 1.7 Fixed JobLevel requirements. [Euphy]
//============================================================
//= Перевод Xsoid
//============================================================

moc_ruins,118,99,5	script	Roberto	88,{
	if (BaseClass == Job_Archer) {
		if (getskilllv("AC_MAKINGARROW") == 1) {
			mes "[Roberto]";
			mes "Ох! Ты из моего родного города! Рад тебя видеть! Как жизнь? Что? Стрела? Ты сделал её сам?";
			next;
			mes "[Roberto]";
			mes "Ха! Ты думаешь что самодельные стрелы самые лучшие? Ну, может быть. Ладно, будет время заходи, всегда рад тебя видить.";
			close;
		}
		else if (JobLevel >= 30 || (BaseJob == Job_Hunter || BaseJob == Job_Bard || BaseJob == Job_Dancer)) {
			mes "[Roberto]";
			mes "Ты поработал..";
			next;
			if ((countitem(907) > 19) && (countitem(921) > 6) && (countitem(906) > 40) && (countitem(1019) > 12) && (countitem(501) > 0)) {
				mes "[Roberto]";
				mes "Ты принёс всё что нужно. Здорово. Приступим к обучению.";
				next;
				delitem 907,20; //Resin
				delitem 921,7; //Mushroom_Spore
				delitem 906,41; //Tough_Scalelike_Stem
				delitem 1019,13; //Wooden_Block
				delitem 501,1; //Red_Potion
				skill "AC_MAKINGARROW",1,SKILL_PERM;
				mes "[Roberto]";
				mes "Теперь можно не волноваться о стрелах.";
				next;
				mes "[Roberto]";
				mes "Ах, да! А ты видел парня по имени Jason в Пайоне. Будь осоторожен, он суровый чувак.";
				close;
			}
			mes "[Roberto]";
			mes "Лучник в Морроке?! Очень рад тебя видеть! Я давно прибыл в эти земли и долгое время нахожусь тут совсем один, в дали от саратников.";
			next;
			switch (select("Давай не раскисай, ты же лучник.:Жизнь боль, все должны страдать.")) {
			case 1:
				mes "[Roberto]";
				mes "Да. Спасибо. В таком месте необходимо быть собранным и вовсеоружии. Что здесь затруднительно, стрелы днём с огнём не сискать. Поэтому я начал делать их сам.";
				next;
				select("Eh, really?!");
				mes "[Roberto]";
				mes "Я собираю разные предметы и делаю из них стрелы. Очень полезный навык. Если что я могу обучить и тебя.";
				next;
				select("That would be wonderful.");
				mes "[Roberto]";
				mes "Но я не буду делать это просто так. Халявы не будет. Для начала мне нужно что бо ты принес некоторые вещи.";
				next;
				mes "[Roberto]";
				mes "Я очень скучаю по моему родному городу. Поэтому мне нужны некоторые вещи которые будут напоминать о нём. Для начала 20 Resins из леса близ Пайона и 1 Red Potion из магазина в городе.";
				next;
				mes "[Roberto]";
				mes "Помимо этого 13 Trunks из Willows которые живут в лесу Пайона, 41 Pointed Scale и 7 Mushroom Spores. Если ничего не забудешь я сделаю то что обещал.";
				next;
				mes "[Roberto]";
				mes "Ну вроде всё. Ступай, я буду ждать тебя.";
				close;
			case 2:
				mes "[Roberto]";
				mes "...ну ты и "+((Sex)?"козёл":"стерва")+".";
				mes "Чеши отсюда.";
				close;
			}
		}
		mes "[Roberto]";
		mes "Хм... У тебя есть что сказать? У меня нет. Ступай себе с миром.";
		close;
	}
	mes "[?]";
	mes "Что? Первый раз видешь лучника? Просто проходи мимо не загораживай мне солнце.";
	close;
}

payon,103,63,3	script	Jason	88,3,3,{
	if (BaseClass == Job_Archer) {
		if (getskilllv("AC_CHARGEARROW") == 1) {
			mes "[Jason]";
			mes "О! Это снова ты. Рад видеть. Всегда рад видеть кого-то снова. Многие больше никогда не возвращаются, это грустно.";
			close;
		}
		else if (JobLevel >= 35 || (BaseJob == Job_Hunter || BaseJob == Job_Bard || BaseJob == Job_Dancer)) {
			mes "[Jason]";
			mes "Чтоб его.. Моя рана не заживает.. Дьявол.. Я был слишком небрежен. Эта рана доставляет много неудобства.";
			next;
			mes "Ты? Ты давно тут? Уровень.. У тебя хороший уровень ты можешь освоить навык Arrow Repel.";
			next;
			switch (select("О чём ты?:Обучи меня.")) {
			case 1:
				mes "[Jason]";
				mes "Ты шутишь? Ты незнаешь что такое Arrow Repel?";
				mes "Ты - странный.";
				next;
				mes "[Jason]";
				mes "Ну хорошо. Я обучу тебя Arrow Repel.";
				next;
				mes "[Jason]";
				mes "Arrow Repel позволяет двигать противника при выстриле. Для использования нужно точно прицелиться. Но в отличия от заклинания прицел не будет фиксироваться на объекте.";
				next;
				mes "[Jason]";
				mes "Этот навык крайне полезен для Лучника который слаб в блюжнем бою. Но для того что бы обучиться ему тебе придется кое что принести мне.";
				next;
				mes "[Jason]";
				mes "И так нужно принести 10 Tentacles, 10 Bill of Birds, 3 Yoyo Tails, 2 Emeralds и наконец очень важно 36 бутылок Banana Juice, который я так любю!";
				next;
				mes "[Jason]";
				mes "Если ты принесеш всё выше сказанное, я обучу тебя Arrow Repel. И даже не думай что ты сможеш обойтись без Бананового сока!";
				close;
			case 2:
				if ((countitem(721) > 1) && (countitem(942) > 2) && (countitem(962) > 9) && (countitem(925) > 9) && (countitem(532) > 35)) {
					mes "[Jason]";
					mes "Вроде всё хорошо. Приступи к обучению Arrow Repel.";
					next;
					delitem 721,2; //Azure_Jewel
					delitem 942,3; //Yoyo_Tail
					delitem 962,10; //Tentacle
					delitem 925,10; //Bill_Of_Birds
					delitem 532,36; //Banana_Juice
					skill "AC_CHARGEARROW",1,SKILL_PERM;
					mes "[Jason]";
					mes "О да. Это даже лучше чем я ожидал. Владей этим навыком. Получай удовольствие от его использования.";
					close;
				}
				mes "[Jason]";
				mes "Ты кого решил абмануть. Нука проверь свой список. 2 Emeralds, 3 Yoyo Tails, 10 Tentacles, 10 Bill of Birds, и 36 бутылок Banana juice!";
				next;
				mes "[Jason]";
				mes "Проваливай и без Бананового сока не возвращайся!";
				close;
			}
		}
		mes "[Jason]";
		mes "Оу. Ты лучник? Здорово, но тебе ещё многому нужно научиться. Приходи ко мне когда немного подрастешь я обучу тебя умению Arrow Repel.";
		close;
	}
	mes "[?]";
	mes "Чего хочет мир от такого одинокого парня как я?";
	close;

OnTouch_:
	mes "[???]";
	mes "Аррррррр...";
	close;
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.0 Roberto message text is based off RO npc.  Jason message
//=     text is custom from old version.
//=     All items are from official quests though.[kobra_k88]
//= 1.0a Now using functions found in "Global_Functions.txt"
//=     for class checks.[kobra_k88]
//= 1.2 Added Baby Class Support [Lupus]
//= 1.3 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 1.4 Fixed exploits [Lupus]
//= 1.4a Fixed some typos [IVBela]
//= 1.4b changed perm. variables to temp ones [Lupus]
//============================================================
