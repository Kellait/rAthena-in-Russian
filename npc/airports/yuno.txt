//===== rAthena Script ======================================= 
//= Yuno Airport Staff
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Yuno Airport Staff
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

y_airport,143,43,5	script	Airport Staff#y_air1a::y_airport1	90,{
	mes "[Сотрудница аэропорта]";
	mes "Добро пожаловать в аэропорт Юно! Мы рады предложить Вам два рейса дирижаблей: внутренний рейс до городов Эйнброх, Лайтхальзен и Хугель, и международный рейс до Рашели и Излюда. Чем могу помочь?";
	next;
	if (select("Сесть на дирижабль.:Отмена.") == 1) {
		mes "[Сотрудница аэропорта]";
		mes "Для всех рейсов стоимость посадки составляет 1 200 зени. Если у Вас есть билет на бесплатный перелёт, он будет использован, и плата взята не будет. Желаете проследовать на посадку?";
		next;
		if (select("Да:Нет") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "y_airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "y_airport",148,51;
				end;
			}
			mes "[Сотрудница аэропорта]";
			mes "К сожалению, у Вас нет 1 200 зен для оплаты посадки.";
			close;
		}
	}
	mes "[Сотрудница аэропорта]";
	mes "Благодарю Вас, всего хорошего.";
	close;
}

y_airport,158,43,5	duplicate(y_airport1)	Airport Staff#y_air1b	90
y_airport,126,43,5	duplicate(y_airport1)	Airport Staff#y_air1c	90

y_airport,143,49,3	script	Arrival Staff#y_air2a::y_airport2	90,{
	mes "[Сотрудница аэропорта]";
	mes "Добро пожаловать в Юно. Если Вы только что сошли с дирижабля, следуйте за мной к выходу в главный зал. В противном случае пройдите, пожалуйста, на посадку.";
	next;
	if (select("Exit to main terminal:Cancel") == 1) {
		mes "[Сотрудница аэропорта]";
		mes "Как только Вы окажетесь в главном зале, вам придётся ещё раз оплачивать посадку, чтобы вернуться на дирижабль. Вам не нужно покидать зону посадки, если город Юно не является конечной целью Вашего перелёта. Желаете выйти в главный зал?";
		next;
		if (select("Да:Нет") == 1) {
			warp "y_airport",142,40;
			end;
		}
	}
	mes "[Сотрудница аэропорта]";
	mes "Благодарим Вас за Вашу поддержку, желаем Вам приятного полёта ~";
	close;
}

y_airport,126,51,3	duplicate(y_airport2)	Arrival Staff#y_air2b	90
y_airport,158,50,3	duplicate(y_airport2)	Arrival Staff#y_air2c	90

y_airport,145,63,5	script	Domestic Boarding	91,{
	mes "[Сотрудница аэропорта]";
	mes "Желаете ли Вы совершить посадку на дирижабль до Эйнброха, Лайтхальзена и Хугеля? Прошу Вас, следуйте за мной.";
	next;
	if (select("Да:Нет") == 1) {
		warp "yuno",59,244;
		end;
	}
	mes "[Сотрудница аэропорта]";
	mes "Хорошо. Благодарим Вас за Вашу поддержку, приятного путешествия ~";
	close;
}

y_airport,140,63,5	script	International Boarding	91,{
	mes "[Сотрудница аэропорта]";
	mes "Желаете ли Вы совершить посадку на дирижабль до Рашели и Излюда? Прошу Вас, следуйте за мной.";
	next;
	if (select("Да:Нет") == 1) {
		warp "yuno",47,244;
		end;
	}
	mes "[Сотрудница аэропорта]";
	mes "Хорошо. Благодарим Вас за то, что Вы с нами и надеемся, что Вам нравится летать нашими дирижаблями.";
	close;
}

yuno,14,262,5	script	Airship Staff#yuno01	91,{
	mes "[Сотрудница аэропорта]";
	mes "Добро пожаловать. На посадку до Излюда (королевство Рун-Мидгард) и до Рашели (республика Арунафельц), пожалуйста, проследуйте сюда.";
	next;
	mes "[Сотрудница аэропорта]";
	mes "Если же Юно является целью Вашего перелёта, спуститесь по лестнице вниз и обратитесь к сотрудникам аэропорта для выхода в главный зал. Благодарим Вас, счастливого путешествия.";
	close;
}

yuno,88,263,3	script	Airship Staff#yuno02	91,{
	mes "[Сотрудница аэропорта]";
	mes "Добро пожаловать. На посадку до Эйнброха, Лайтхальзена и Хугеля (республика Шварцвальт), пожалуйста, проследуйте сюда.";
	next;
	mes "[Сотрудница аэропорта]";
	mes "Если же Юно является целью Вашего перелёта, спуститесь по лестнице вниз и обратитесь к сотрудникам аэропорта для выхода в главный зал. Благодарим Вас, счастливого пути.";
	close;
}
