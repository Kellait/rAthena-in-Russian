//===== rAthena Script ======================================= 
//= Einbroch Airport Staff
//===== By: ================================================== 
//= L0ne_W0lf, Muad_Dib
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Einbroch Airport Staff
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

airport,143,43,5	script	Airport Staff#airport1a::airport1	90,{
	mes "[Сотрудница аэропорта]";
	mes "Добро пожаловать в Аэропорт города Эйнброха. Мы рады предложить Вам рейс до следующих городов:";
	mes "Юно,";
	mes "Лайтхальзен,";
	mes "Хугель.";
	next;
	if (select("Посадка:Отмена") == 1) {
		mes "[Сотрудница аэропорта]";
		mes "Полёт на дирижабле стоит 1 200 зени. Если у Вас есть билет на бесплатный перелёт, он будет использован, и плата взята не будет. Желаете проследовать на посадку?";
		next;
		if (select("Да:Нет") == 1) {
			if (countitem(7311) > 0) {
				delitem 7311,1; //Free_Flying_Ship_Ticket
				warp "airport",148,51;
				end;
			}
			if (Zeny >= 1200) {
				set Zeny, Zeny - 1200;
				warp "airport",148,51;
				end;
			}
			mes "[Сотрудница аэропорта]";
			mes "К сожалению, у Вас недостаточно денег для посадки и нет права на бесплатный перелёт. Позвольте напомнить: посадка стоит 1 200 зен.";
			close;
		}
	}
	mes "[Сотрудница аэропорта]";
	mes "Благодарю Вас, всего наилучшего.";
	close;
}

airport,158,43,5	duplicate(airport1)	Airport Staff#airport1b	90
airport,126,43,5	duplicate(airport1)	Airport Staff#airport1c	90

airport,143,49,3	script	Arrival Staff#airport2a::airport2	90,{
	mes "[Сотрудница аэропорта]";
	mes "Добро пожаловать в Эйнброх. Если Вы только что сошли с дирижабля, следуйте, пожалуйста, за мной к выходу в главный зал. В противном случае выход к дирижаблю маршрута Юно - Хугель - Лайтхальзен находится слева от меня.";
	next;
	if (select("Пройти в зал.:Отмена.") == 1) {
		mes "[Сотрудница аэропорта]";
		mes "Как только Вы окажетесь в главном зале, вам придётся ещё раз оплачивать посадку, чтобы вернуться на дирижабль. Вам не нужно покидать зону посадки, если город Эйнброх не является конечной целью Вашего перелёта. Желаете выйти в главный зал?";
		next;
		if (select("Да:Нет") == 1) {
			warp "airport",142,40;
			end;
		}
	}
	mes "[Сотрудница аэропорта]";
	mes "Благодарим Вас за Вашу поддержку, желаем Вам приятного полёта ~";
	close;
}

airport,126,51,3	duplicate(airport2)	Arrival Staff#airport2b	90
airport,158,50,3	duplicate(airport2)	Arrival Staff#airport2c	90

einbroch,94,267,3	script	Airship Staff#ein01	91,{
	mes "[Сотрудница аэропорта]";
	mes "Добро пожаловать в эйнброхский аэропорт. Пожалуйста, чтобы попасть на дирижабль до Юно, Лайтхальзена и Хугеля, проследуйте в эту дверь.";
	next;
	mes "[Сотрудница аэропорта]";
	mes "В противном случае, если город Эйнброх является целью Вашего перелёта, проследуйте по лестнице вниз и обратитесь к сотрудникам аэропорта для выхода в главный зал. Благодарю Вас, счастливого путешествия.";
	close;
}
