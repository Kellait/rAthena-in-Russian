//===== rAthena Script =======================================
//= Eden Group Quests - Mission [11 - 25]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 11 - 25.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Bug fixes. [Capuche]
//============================================================
//= Translated en -> ru [Akaineko] 
//= Актуализация до 1.3 [Fenrir]
//============================================================

moc_para01,36,38,3	script	Mission [11 - 25]#Tuto	4_BOARD3,{
	if (countitem(6219) < 1) {// Para_Team_Mark
		mes "^4d4dff- Чтобы участвовать в этих -^000000";
		mes "^4d4dff- заданиях, Вам нужен значок -^000000";
		mes "^4d4dff- группы ''Эдем''. -^000000";
		close;
	}
	for ( .@i = 11124; .@i < 11134; .@i++ )
		if (checkquest(.@i,PLAYTIME) == 2) erasequest .@i;
	for( .@i = 11114; .@i < 11124; .@i++ ) {
		.@j = checkquest(.@i,HUNTING);
		if (.@j == 2) {
			mes " - Вам нужно сдать отчёт о - ";
			mes " - проделанной работе, прежде - ";
			mes " - чем браться за новое дело. - ";
			mes " ";
			mes " - Ответственный : Спайк - ";
			close;
		}
		else if (.@j >= 0) {
			mes " - Вы можете иметь в активе - ";
			mes " - только одно задание. - ";
			mes " ";
			mes " - Ответственный : Спайк - ";
			close;
		}
	}
	if (BaseLevel < 11) {
		mes " - На данный момент для Вас нет заданий. - ";
		close;
	}
	if (BaseLevel > 25) {
		mes " - Эти задания для Вас - ";
		mes " - слишком просты. Поищите - ";
		mes " - что-нибудь более подходящее. - ";
		close;
	}
	mes " - Доска заданий -";
	mes " - для новичков. -";
	mes " ";
	mes " - Ответственный : Спайк -";
	next;
	switch(select("Охота на пчёл:Охота на кондоров:Лапки кузнечиков:Охота на хвостов:Поиски спор:Контроль паразитов:Охота на мук:Охота на летучих мышей:Сбор перьев:Сбор ядовитых спор:Отмена")) {
		case 1: callsub L_Quest,11114,"Охота на пчёл"," - Победить 10 пчёл (^4d4dff"+getmonsterinfo(1004,MOB_NAME)+"^000000) - "," - к северо-западу от Пронтеры. - ";
		case 2: callsub L_Quest,11115,"Охота на кондоров"," - Победить 20 кондоров (^4d4dff"+getmonsterinfo(1009,MOB_NAME)+"^000000) - "," - к югу от Моррока. - ";
		case 3: callsub L_Quest,11116,"Лапки кузнечиков"," - Победить 10 кузнечиков (^4d4dff"+getmonsterinfo(1052,MOB_NAME)+"^000000) - "," - к юго-западу от Пронтеры и - "," - принести 10 лапок кузнечика (^4d4dff"+getitemname(940)+"^000000). - ";
		case 4: callsub L_Quest,11117,"Охота на хвостов"," - Победить 20 хвостов (^4d4dff"+getmonsterinfo(1024,MOB_NAME)+"^000000) - "," - к юго-востоку от Пайона.- ";
		case 5: callsub L_Quest,11118,"Поиски спор"," - Победить 30 спор (^4d4dff"+getmonsterinfo(1014,MOB_NAME)+"^000000) - "," - в окрестностях Пайона. - ";
		case 6: callsub L_Quest,11119,"Контроль паразитов"," - Ликвидировать 20 тараканьих яиц (^4d4dff"+getmonsterinfo(1048,MOB_NAME)+"^000000) - "," - в канализации Пронтеры - "," - и принести 10 личинок (^4d4dff"+getitemname(915)+"^000000). - ";
		case 7: callsub L_Quest,11120,"Охота на мук"," - Победить 20 мук (^4d4dff"+getmonsterinfo(1055,MOB_NAME)+"^000000) - "," - к югу от Пронтеры. - ";
		case 8: callsub L_Quest,11121,"Охота на летучих мышей"," - Победить 20 фамильяров (^4d4dff"+getmonsterinfo(1005,MOB_NAME)+"^000000) - "," - в канализации Пронтеры - "," - к востоку от Пронтеры. - ";
		case 9: callsub L_Quest,11122,"Сбор перьев"," - Победить 30 пеко-пеко (^4d4dff"+getmonsterinfo(1019,MOB_NAME)+"^000000) - "," - к югу от Пронтеры. - ";
		case 10: callsub L_Quest,11123,"Сбор ядовитых спор"," - Победить 30 ядовитых спор (^4d4dff"+getmonsterinfo(1077,MOB_NAME)+"^000000) - "," - в Геффенской Башне - "," - принести 5 ядовитых спор (^4d4dff"+getitemname(7033)+"^000000). - ";
		case 11: mes " - Вы закончили. - "; close;
	}
	end;
L_Quest:
	if (checkquest(getarg(0)+10,PLAYTIME) == 1) {
		mes " - Задание ^4d4dff"+ getarg(1) +"^000000 - ";
		mes " - на сегодня выполнено. - ";
		mes " ";
		mes " - Ответственный : Спайк - ";
		close;
	}
	mes " - Задание '"+ getarg(1) +"' - ";
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	mes " ";
	mes " - Ответственный : Спайк - ";
	next;
	if (select("Я этим займусь.:Поискать другое.") == 2) {
		mes " - Попробуем найти -";
		mes " - другое задание. - ";
		close;
	}
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	setquest getarg(0);
	close;
}

moc_para01,32,30,6	script	Spike	4_F_CHILD,{
	for( .@i = 11114; .@i < 11124; .@i++ ) {
		.@j = checkquest(.@i,HUNTING);
		if (.@j == 2) {
			switch(.@i) {
				case 11114: callsub L_Quest,.@i,"Охота на пчёл",900;
				case 11115: callsub L_Quest,.@i,"Охота на кондоров",1800;
				case 11116: callsub L_Quest,.@i,"Лапки кузнечиков",1400,940,10;
				case 11117: callsub L_Quest,.@i,"Охота на хвостов",2600;
				case 11118: callsub L_Quest,.@i,"Поиски спор",3900;
				case 11119: callsub L_Quest,.@i,"Контроль паразитов",2900,915,10;
				case 11120: callsub L_Quest,.@i,"Охота на мук",3200;
				case 11121: callsub L_Quest,.@i,"Охота на летучих мышей",3200;
				case 11122: callsub L_Quest,.@i,"Сбор перьев",5400;
				case 11123: callsub L_Quest,.@i,"Сбор ядовитых спор",6000,7033,5;
			}
		}
		else if (.@j >= 0) {
			mes "[Спайк]";
			mes "Ты ещё не завершил"+(Sex?"":"а")+" задание. Не опускай рук, осталось немного.";
			close;
		}
	}
	mes "[Спайк]";
	mes "Ищешь, чем бы заняться? У нас всегда найдётся работа...";
	next;
	mes "[Спайк]";
	mes "Осмотрись, и ты наверняка найдёшь уйму всего полезного.";
	close;

L_Quest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id,HUNTING) == 2) {
		.@item_req = getarg(3,0);
		.@req_amount = getarg(4,0);
		if (.@item_req) {
			if (countitem(.@item_req) < .@req_amount) {
				mes "[Спайк]";
				mes "У тебя недостаточно "+ getitemname(.@item_req) +".";
				mes "Тебе нужно "+ .@req_amount +" "+ getitemname(.@item_req) +".";
				close;
			}
		}
		mes "[Спайк]";
		mes "Хмм... Ну-ка ~";
		mes "Ты выполнил"+(Sex?"":"а")+" задание ''"+getarg(1)+"''.";
		next;
		mes "[Спайк]";
		mes "Прекрасно ~!!";
		mes "Заказчик будет тобой очень доволен. Он высоко ценит труды.";
		mes "Мы будем рады принять твою помощь в следующий раз!";
		if (.@item_req)
			delitem .@item_req,.@req_amount;
		erasequest .@quest_id;
		setquest .@quest_id+10;
		getexp getarg(2),0;
		close;
	}
}
