//===== rAthena Script =======================================
//= Eden Group Quests - Mission [41 - 55]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 41 - 55.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.4 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================
//= Translated en -> ru [Akaineko]
//============================================================

moc_para01,40,38,3	script	Задания [41 - 55]	857,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "Вы не относитесь к членам группы ''Эдем''. Чтобы получить доступ к этим заданиям, вам нужно зарегистрироваться.";
		close;
	}
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 1000) {
		mes "^008800Секундочку!";
		mes "Ваш инвентарь почти заполнен. Освободите 1000 единиц веса и возвращайтесь.^000000";
		close;
	}
	if (checkquest(12088) == -1) {
		if (BaseLevel < 41 || BaseLevel > 55) {
			mes "Доска заданий для авантюристов 41-55 уровней.";
			close;
		}
		mes "[Доска заданий]";
		mes "На доске множество заданий.";
		next;
		if(select("Ознакомиться с заданиями.:Отмена.") == 2) {
			mes "^000077Посмотрим задания позже.^000000.";
			close;
		}
		switch(select("Охота на лягушек:Охота на оловянных солдатиков:Охота на кукри:Охота на орчат:Охота на орков:Охота на вадонов:Охота на мегалодонов:Охота на марсов:Охота на корнутусов:Охота на туман:Добыча устриц:Охота на морские сферы:Добыча фена:Орки-скелеты:Зенорки:Охота на мумий")) {
			case 1: callsub L_Quest,12072,"Доводилось ли вам встречать лягушек с красной кожей? Они называются ^111199"+getmonsterinfo(1034,MOB_NAME)+"^000000 и являются очень редкими. Мы пытаемся подтвердить ареал их обитания. Пожалуйста, изучите 20 особей.";
			case 2: callsub L_Quest,12073,"Слышал, бывают игрушки, которые нападают на людей. Это просто смешно! Покажем им нашу силу! Нужно убить как минимум 15 оловянных солдатиков (^111199"+getmonsterinfo(1248,MOB_NAME)+"^000000)!";
			case 3: callsub L_Quest,12074,"Кого-то обокрали неподалёку от пляжа! Предполагаемый подозреваемый -- монстр вне закона по имени Кукри. Пожалуйста, убейте 30 ^111199"+getmonsterinfo(1070,MOB_NAME)+"^000000.";
			case 4: callsub L_Quest,12075,"Орчата (^111199"+getmonsterinfo(1686,MOB_NAME)+"^000000) в наше время всё более жестоки. Убейте 15 шт.";
			case 5: callsub L_Quest,12076,"Орочьи воины (^111199"+getmonsterinfo(1023,MOB_NAME)+"^000000) становятся всё агрессивнее. Убейте 20 шт.";
			case 6: callsub L_Quest,12077,"Видели крабов с толстым панцирем? Они называются ^111199"+getmonsterinfo(1066,MOB_NAME)+"^000000. Вам нужно добыть 15 этих вадонов.";
			case 7: callsub L_Quest,12078,"Повсюду полно дохлой рыбы, и ситуация только ухудшается. Убейте хотя бы 30 мегалодонов (^111199"+getmonsterinfo(1064,MOB_NAME)+"^000000).";
			case 8: callsub L_Quest,12079,"Не могли бы вы добыть мне несколько марсов (^111199"+getmonsterinfo(1144,MOB_NAME)+"^000000)? Мне очень нужно как минимум 15 шт. для исследований, пожалуйста, помогите.";
			case 9: callsub L_Quest,12080,"Дурацкий корнутус укусил мэра за ногу! Мэр в бешенстве, и, чтобы его умилостивить, нам нужно укусить 20 корнутусов (^111199"+getmonsterinfo(1067,MOB_NAME)+"^000000)!";
			case 10: callsub L_Quest,12081,"Туманы (^111199"+getmonsterinfo(1151,MOB_NAME)+"^000000) сводят людей с ума. Они похожи на призраков и пугают народ. Избавьтесь от 15 туманов.";
			case 11: callsub L_Quest,12082,"Не спрашивайте, зачем, просто добудьте 30 устриц (^111199"+getmonsterinfo(1074,MOB_NAME)+"^000000), пожалуйста. Это секретная миссия!";
			case 12: callsub L_Quest,12083,"Морские сферы, ^111199"+getmonsterinfo(1142,MOB_NAME)+"^000000? Вы знаете, что это? У меня есть для вас задание, 15 морских сфер, сделайте, пожалуйста.";
			case 13: callsub L_Quest,12084,"Мэр желает откушать изысканное рыбное блюдо. Лучшей рыбой для него будет рыба фен (^111199"+getmonsterinfo(1158,MOB_NAME)+"^000000). Пожалуйста, поймайте 20 штук!";
			case 14: callsub L_Quest,12085,"Убитые орки вернулись в нашу деревню немёртвыми скелетами и сеют хаос! Убейте как минимум 15 орков-скелетов (^111199"+getmonsterinfo(1152,MOB_NAME)+"^000000).";
			case 15: callsub L_Quest,12086,"На нас нападают какие-то чудные монстры, называются зенорками (^111199"+getmonsterinfo(1177,MOB_NAME)+"^000000). Убейте 30 зенорков.";
			case 16: callsub L_Quest,12087,"Предупреждаю! Это жестокая миссия! Убить 15 мумий!!! Я однажды пробовал, но не смог. Слишком страшно!";
		}
		end;
	}
	setarray .@Exp[0],9600,7200,15750,7875,11100,8550,17100,9000,12300,9900,20250,10350,13800,11025,22500,11250;
	for(set .@i,0; .@i<16; set .@i,.@i+1)
		if (checkquest(12072+.@i,HUNTING) == 2) {
			mes "[Доска заданий]";
			mes "Вы выполнили задание.";
			mes "Вот ваше вознаграждение.";
			erasequest 12088;
			erasequest 12072+.@i;
			specialeffect2 EF_STEAL;
			getexp .@Exp[.@i],0;
			close;
		}
	if ((checkquest(12072,HUNTING) == 0) && (checkquest(12073,HUNTING) == 0) && (checkquest(12074,HUNTING) == 0) && (checkquest(12075,HUNTING) == 0) && (checkquest(12076,HUNTING) == 0) && (checkquest(12077,HUNTING) == 0) && (checkquest(12078,HUNTING) == 0) && (checkquest(12079,HUNTING) == 0) && (checkquest(12080,HUNTING) == 0) && (checkquest(12081,HUNTING) == 0) && (checkquest(12082,HUNTING) == 0) && (checkquest(12083,HUNTING) == 0) && (checkquest(12084,HUNTING) == 0) && (checkquest(12085,HUNTING) == 0) && (checkquest(12086,HUNTING) == 0) && (checkquest(12087,HUNTING) == 0)) {
		mes "Теперь вы можете взглянуть на задания.";
		erasequest 12088;
		close;
	}
	mes "Здесь не на что смотреть.";
	close;
L_Quest:
	mes "[Доска заданий]";
	mes getarg(1);
	next;
	if(select("Взять задание.:Отмена.") == 1) {
		mes "[Доска заданий]";
		mes "Вы приняли это задание. Детальное описание можно найти в окне заданий [alt + Г].";
		setquest 12088;
		setquest getarg(0);
	}
	close;
}
