//===== rAthena Script ======================================= 
//= Eden Group Quests - Common NPCs
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 2.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Eden Group Headquarter NPC's.
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Removed showevent use, and use of duplicates.
//=     Commented out warps that should not be active.
//= 1.2 Reduced 300+ copy-paste lines to 1 duplicated NPC. [Brian]
//= 1.3 Some little optimization here and there. [Masao]
//= 1.4 Updated to match the new Izlude Map. [Masao]
//= 1.5 Added some missing Eden Group Teleport Officers. [Masao]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//= 1.7 Added Malaya teleporter. [Euphy]
//= 1.8 Updated to match the latest official script. [Euphy]
//= 1.9 Added support to Para_Team_Mark_ in Eden Group. [AtlantisRO/Ragno]
//= 2.0 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================
//= Translated en -> ru [Akaineko] 
//============================================================

// Common NPCs :: lv_tre
//============================================================
moc_para01,27,35,5	script	Secretary Lime Evenor	952,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 2000) {
		mes "- Минуточку !! -";
		mes "- В данный момент вы перегружены.-";
		mes "- Возвращайтесь, избавившись от -";
		mes "- лишнего груза. -";
		close;	}
	if (countitem(6219) > 0) {
		mes "[Лаим Ивенор]";
		mes "Благодаря всем гостям и членам нашей группы мы недавно смогли подготовить новую версию значка.";
		mes "Теперь можно обменять старые значки группы ''Эдем'' на новые.";
		next;
		mes "[Лаим Ивенор]";
		mes "Хотя и с некоторым ограничением по частоте использования, новый значок телепортирует вас в место вашего сохранения.";
		mes "Желаете обменять свой?";
		next;
		if (select("Разумеется.:Мне и так неплохо.") == 2){
			mes "[Лаим Ивенор]";
			mes "Есть ли у вас какая-то серьёзная причина отказываться?";
			close;
		}
		delitem 6219,1;
		getitem 22508,1;
		mes "[Лаим Ивенор]";
		mes "Пожалуйста. До свидания~";
		close;
	}
	mes "[Лаим Ивенор]";
	mes "Прислушайтесь, те, кто следует за своей мечтой! Мы представители райского уголка, который называется группа ''Эдем''. Второго такого места не найти.";
	mes "Здравствуйте. Чем могу помочь?";
	next;
	while (1) {
		switch (select("Что такое группа ''Эдем''?:Присоединиться.:Зарегистрировать новую миссию.:Поиск миссий.:Отмена.")) {
		case 1:
			mes "[Лаим Ивенор]";
			mes "Группа ''Эдем'' обьединяет тех, кто предлагает свою помощь нуждающимся людям.";
			next;
			mes "[Лаим Ивенор]";
			mes "Если человеку нужна помощь, мы заводим дело и вешаем его на доску заданий. Так наши люди получают возможность узнать об этом.";
			next;
			mes "[Лаим Ивенор]";
			mes "Они изучают доступные задания и делают, что могут. За выполнение заданий даётся награда.";
			next;
			mes "[Лаим Ивенор]";
			mes "Если Вы не слишком знакомы с Миром, это отличный способ изучить его. А если Вы чувствуете себя уверенно, то Вы сможете встретить новых друзей, завести новые знакомства. Ваше имя будет известно повсеместно.";
			next;
			break;
		case 2:
			if (!callfunc("F_HasEdenGroupMark")) {
				mes "[Лаим Ивенор]";
				mes "Для этого Вам нужно просто пройти у меня процедуру регистрации.";
				mes "Будете регистрироваться?";
				next;
				switch (select("Да, буду.:Не хочу.")) {
				case 1:
					mes "[Лаим Ивенор]";
					mes "Хорошо. Чудесно! Впишите вот здесь своё имя.";
					input .@input$;
					next;
					mes "[Лаим Ивенор]";
					mes "Всё? Дайте взглянуть.";
					next;
					mes "[Лаим Ивенор]";
					mes "Вас зовут ^3131FFХo"+.@input$+"гa^000000...? Правильно?";
					mes "Да??";
					next;
					emotion e_swt;
					mes "[Лаим Ивенор]";
					mes "Хм, вы не так писали?..";
					mes "Хм, немного неразборчиво.";
					next;
					mes "[Лаим Ивенор]";
					mes "Напишите, пожалуйста, ещё раз. На этот раз, если можно, ^3131FFпоразборчивее^000000.";
					input .@input$;
					next;
					mes "[Лаим Ивенор]";
					mes "Ах~ ха. Вас зовут ^3131FF"+strcharinfo(0)+"^000000.";
					mes "На этот раз я прочла правильно.";
					next;
					emotion e_no1;
					mes "[Лаим Ивенор]";
					mes "Ваше имя внесено в списки. Добро пожаловать в Райский уголок!";
					next;
					mes "[Лаим Ивенор]";
					mes "Надеюсь, так из Вас выйдет толк.";
					getitem 22508,1; //Para_Team_Mark_
					next;
					break;
				case 2:
					mes "[Лаим Ивенор]";
					mes "У Вас есть вопросы о группе?";
					next;
					break;
				}
			} else {
				mes "[Лаим Ивенор]";
				mes "Вы уже в списках членов группы.";
				next;
			}
			break;
		case 3:
			mes "[Лаим Ивенор]";
			mes "Хотите зарегистрировать миссию, чтобы мы её выполнили?";
			next;
			if(select("Да.:Нет.") == 1) {
				mes "[Лаим Ивенор]";
				mes "Хорошо. Вот здесь впишите своё имя.";
				input .@input$;
				next;
				mes "[Лаим Ивенор]";
				mes "^3131FF"+strcharinfo(0)+"^000000.. Это ваше имя?";
				mes "Ну и почерк, ничего не понимаю.";
				next;
				mes "[Лаим Ивенор]";
				mes "^3131FF"+strcharinfo(0)+"^000000, теперь расскажите, что следует делать. На какую карту идти?";
				input .@inputstr$;
				next;
				mes "[Лаим Ивенор]";
				mes "^3131FFКарта задания: "+.@inputstr$+"^000000.";
				mes "Хм, учтём.";
				next;
				mes "[Лаим Ивенор]";
				mes "Теперь кратко опишите суть задания.";
				mes "Например: ''Победить 10 порингов''.";
				input .@input$;
				next;
				mes "[Лаим Ивенор]";
				mes "Справились?";
				mes "Ну-ка.";
				next;
				mes "[Лаим Ивенор]";
				mes "Кхм...........";
				next;
				mes "[Лаим Ивенор]";
				mes "Хм.. Хмм??..................";
				next;
				emotion e_dots;
				mes "[Лаим Ивенор]";
				mes "Знаете, прежде чем регистрировать новые, Вам совершенно неоходимо выполнить одну миссию...";
				next;
				emotion e_pif;
				mes "[Лаим Ивенор]";
				mes "^3131FFЗаказчик: Лаим Ивенор^000000";
				mes "^3131FFЗадание: Упражнения в чистописании в течение одного месяца.^000000";
				mes "Ваш почерк просто ужасен!";
				next;
				mes "[Лаим Ивенор]";
				mes "^3131FFЗадание: "+.@input$+".^000000";
				mes "Так или иначе, мы закончили с регистрацией.";
				next;
				mes "[Лаим Ивенор]";
				mes "На данный момент задания полностью укомплектованы. Дождитесь, пока освободится место и до вас дойдёт очередь.";
				next;
			}
			break;
		case 4:
			if (callfunc("F_HasEdenGroupMark")) {
				mes "[Лаим Ивенор]";
				mes "Решили взяться за наши задания?";
				next;
				mes "[Лаим Ивенор]";
				mes "Справа от стойки находятся доски обьявлений, там множество заданий, описания, требования.";
				next;
			}
			else {
				mes "[Лаим Ивенор]";
				mes "Чтобы брать задания, Вам нужно зарегистрироваться.";
				close;
			}
			break;
		case 5:
			mes "[Лаим Ивенор]";
			mes "Если у Вас возникнут какие-то вопросы, обращайтесь.";
			close;
		}
	}
	close;
}

-	script	Eden Teleport Officer#0::eto	-1,{
	mes "[Привратница Эдема]";
	mes "Бродяги, бесцельно скитающиеся по Миру, подойдите и прислушайтесь! Компания Эдем: Райский уголок здесь для того, чтобы оказать вам поддержку и показать прекрасное будущее.";
	next;
	mes "[Привратница Эдема]";
	mes "Тебе нечем заняться? Ты молод и талантлив, но никто не предлагает работу? Хочешь стать профи в решении проблем? Тогда Компания Эдем решит твою проблему.";
	next;
	switch (select("Отправиться в Эдем.:Не хочу ни с кем общаться.")) {
	case 1:
		mes "[Привратница Эдема]";
		mes "Пойдём в наш тайный штаб!";
		set nak_warp,strnpcinfo(2);
		close2;
		warp "moc_para01",31,14;
		end;
	case 2:
		mes "[Привратница Эдема]";
		mes "Если не знаешь, что делать со своим будущим, не стесняйся мне на него пожаловаться. Приходи, поговорим?";
		close;
	}
}
prontera,124,76,3	duplicate(eto)	Eden Teleport Officer#1	729
moc_ruins,68,164,3	duplicate(eto)	Eden Teleport Officer#2	729
geffen,132,66,3	duplicate(eto)	Eden Teleport Officer#3	729
alberta,124,67,3	duplicate(eto)	Eden Teleport Officer#4	729
aldebaran,133,119,5	duplicate(eto)	Eden Teleport Officer#5	729
izlude_in,68,162,1	duplicate(eto)	Eden Teleport Officer#6	729
prt_church,103,78,3	duplicate(eto)	Eden Teleport Officer#7	729
geffen_in,160,104,5	duplicate(eto)	Eden Teleport Officer#8	729
moc_prydb1,53,126,3	duplicate(eto)	Eden Teleport Officer#9	729
alberta_in,75,39,3	duplicate(eto)	Eden Teleport Officer#10	729
payon_in02,58,58,1	duplicate(eto)	Eden Teleport Officer#11	729
payon,177,111,3	duplicate(eto)	Eden Teleport Officer#12	729
que_ng,33,73,3	duplicate(eto)	Eden Teleport Officer#13	729
que_ng,144,161,5	duplicate(eto)	Eden Teleport Officer#14	729
yuno,144,189,5	duplicate(eto)	Eden Teleport Officer#15	729
rachel,125,144,3	duplicate(eto)	Eden Teleport Officer#16	729
comodo,202,151,4	duplicate(eto)	Eden Teleport Officer#17	729
hugel,93,153,4	duplicate(eto)	Eden Teleport Officer#18	729
veins,220,109,4	duplicate(eto)	Eden Teleport Officer#19	729
einbroch,250,211,4	duplicate(eto)	Eden Teleport Officer#20	729
lighthalzen,164,86,4	duplicate(eto)	Eden Teleport Officer#21	729
amatsu,100,145,5	duplicate(eto)	Eden Teleport Officer#22	729
ayothaya,221,191,3	duplicate(eto)	Eden Teleport Officer#23	729
louyang,224,107,3	duplicate(eto)	Eden Teleport Officer#24	729
gonryun,162,122,5	duplicate(eto)	Eden Teleport Officer#25	729
moscovia,209,197,5	duplicate(eto)	Eden Teleport Officer#26	729
brasilis,191,224,3	duplicate(eto)	Eden Teleport Officer#27	729
dewata,192,193,5	duplicate(eto)	Eden Teleport Officer#28	729
morocc,161,97,5	duplicate(eto)	Eden Teleport Officer#29	729
izlude,131,148,4	duplicate(eto)	Eden Teleport Officer#30	729
izlude_a,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_a	729
izlude_b,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_b	729
izlude_c,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_c	729
izlude_d,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_d	729
umbala,105,158,3	duplicate(eto)	Eden Teleport Officer#31	729
malaya,238,206,6	duplicate(eto)	Eden Teleport Officer#32	729

moc_para01,30,10,0	script	#eden_out	45,1,1,{
OnTouch:
	switch (nak_warp) {
		case 1: warp "prontera",116,72; end;
		case 2: warp "moc_ruins",64,161; end;
		case 3: warp "geffen",120,39; end;
		case 4: warp "alberta",117,56; end;
		case 5: warp "aldebaran",168,112; end;
		case 6: warp "izlude_in",73,165; end;
		case 7: warp "prt_church",99,78; end;
		case 8: warp "geffen_in",162,99; end;
		case 9: warp "moc_prydb1",51,118; end;
		case 10: warp "alberta_in",73,43; end;
		case 11: warp "payon_in02",64,60; end;
		case 12: warp "payon",161,58; end;
		case 13: warp "que_ng",33,63; end;
		case 14: warp "que_ng",144,166; end;
		case 15: warp "yuno",158,125; end;
		case 16: warp "rachel",115,125; end;
		case 17: warp "comodo",192,145; end;
		case 18: warp "hugel",88,148; end;
		case 19: warp "veins",216,104; end;
		case 20: warp "einbroch",246,204; end;
		case 21: warp "lighthalzen",159,95; end;
		case 22: warp "amatsu",110,150; end;
		case 23: warp "ayothaya",217,178; end;
		case 24: warp "louyang",217,103; end;
		case 25: warp "gonryun",155,120; end;
		case 26: warp "moscovia",218,198; end;
		case 27: warp "brasilis",190,220; end;
		case 28: warp "dewata",192,182; end;
		case 29: warp "morocc",161,97; end;
		case 30: warp "izlude",134,118; end;	// Old coordinates: (127,142)
		case 31: warp "umbala",94,154; end;
		case 32: warp "malaya",234,199; end;
		default: warp "prontera",116,72; end;
	}
	end;
}

moc_para01,47,39,3	script	#warp_2_pass	111,{
	if (callfunc("F_HasEdenGroupMark")) {
		mes "[Лаим Ивенор]";
		mes "Это место предназначено исключительно для членов Райского уголка. Можете пользоваться им, сколько душе угодно!";
		next;
		switch (select("Войти.:Не входить.")) {
		case 1:
			mes "Дверь отделана шикарно, но смотрится немного тяжеловесно.";
			close2;
			warp "moc_para01",106,14;
			end;
		case 2:
			mes "[Лаим Ивенор]";
			mes "Как хотите.";
			close;
		}
	}
	mes "[Лаим Ивенор]";
	mes "О, это место предназначено исключительно для членов Райского уголка. Если хотите внутрь -- зарегистрируйтесь.";
	close;
}

moc_para01,16,22,7	script	Old Adventurer#eden	900,{
	mes "[Ветеран]";
	mes "Ёлки-палки! Я ещё не настолько стар! Как там зовут эту крысу?.. Лайм Эвенор?.. За развалюху-маразматика меня держит!";
	next;
	mes "[Ветеран]";
	mes "Я хочу сюда вступить! Так нечестно!";
	mes "Даже ты думаешь, что я старый маразматик?..";
	close;
}

moc_para01,182,48,3	script	Eden's Chief#eden	886,{
	set .@eggf,rand(1,118);
	set .@eggf_1,.@eggf + 1;
	set .@eggf_2,.@eggf + 2;
	set .@eggf_6,.@eggf + 6;
	mes "[Эдемский повар]";
	mes ""+.@eggf+".. "+.@eggf_1+"... "+.@eggf_2+".... "+.@eggf_6+"..?";
	next;
	emotion e_sob;
	mes "[Эдемский повар]";
	mes "Ох, чёрт! Сколько яиц я вбухнул?.. Арр!!! Опять сбился!";
	next;
	emotion e_sob;
	mes "[Эдемский повар]";
	mes "На кой чёрт мне сказали это делать! Что за чёртово задание такое!";
	next;
	emotion e_sob;
	mes "[Эдемский повар]";
	mes "Какие черти настолько проголодались? Никто столько не сьест. Наверное, думают, что мне заняться больше нечем!";
	close;
}

moc_para01,172,28,5	script	Eden's Intern#eden	883,{
	mes "[Эдемский интерн]";
	mes "Знаешь, что я понял?";
	next;
	mes "[Эдемский интерн]";
	mes "Либо из шкуры вон лезешь -- либо не справился.";
	next;
	mes "[Эдемский интерн]";
	mes "А мне ещё всю фармацевтику учить.";
	next;
	mes "[Эдемский интерн]";
	mes "Я не справлюсь. Не могу просто.";
	close;
}

moc_para01,20,35,5	script	Office Assistant Neede	814,{
	mes "[Нидэ]";
	mes "Я пришла в Компанию несколько лет назад, а кажется, будто вчера. Здесь постоянно столько работы, что теряешь счёт годам.";
	next;
	mes "[Нидэ]";
	mes "Поначалу каждый день был как на войне.";
	next;
	mes "[Нидэ]";
	mes "Повар из ^3131FFстоловой справа^000000 вбухивает в еду столько специй, что даже здесь воняет. Приходится постоянно работать с открытым окном, иначе помереть можно!";
	next;
	mes "[Нидэ]";
	mes "А видел"+(Sex?"":"а")+" ^3131FFтренировочный зал^000000 в кресте ^3131FFсевернее холла^000000? Там иногда такой шум стоит, что у нас тут стены ходуном ходят!";
	next;
	emotion e_ag;
	mes "[Нидэ]";
	mes "А девка со ^3131FFвторого этажа^000000 всё время воет. Я скоро с ума сойду.";
	next;
	mes "[Нидэ]";
	mes "А вон та странная баба -- это вообще наш начальник...";
	next;
	mes "[Нидэ]";
	mes "Не представляю себе, кто она. Всё время кричит-ругается. А однажды я слышала: ''Бах!'' и звуки как будто ударов... Не хочу даже думать, что это было.";
	next;
	emotion e_omg;
	mes "[Нидэ]";
	mes "Ой!";
	mes "Тсс! Только никому не говори, это тайна!";
	next;
	mes "[Нидэ]";
	emotion e_sigh;
	mes "А этой Ивенор я не доверяю. Никогда не поймёшь, что у неё на уме... Наверняка доносит всё боссу.";
	next;
	emotion e_pif,0,"Secretary Lime Evenor";
	mes "[Лаим Ивенор]";
	mes "Я не такая.";
	next;
	emotion e_wah;
	mes "[Нидэ]";
	mes "Ах? Она слышала? Что же делать~!";
	close;
}

function	script	F_HasEdenGroupMark	{
	return (countitem(6219) > 0 || countitem(22508) > 0);
}
