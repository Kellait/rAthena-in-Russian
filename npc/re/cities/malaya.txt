//===== rAthena Script ======================================= 
//= Port Malaya
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Port Malaya Town Script
//===== Additional Comments: ================================= 
//= 0.1 Alberta sailors only. [Euphy]
//= 0.2 Adapted from Masao's conversion.
//= 1.0 Added remaining NPCs.
//= 1.1 Added Jeepney script. [DeadlySilence]
//============================================================ 
// Перевод Lepricon

// Port Malaya Transportation
//============================================================
// Old coordinates: alberta (237,71)
alberta,196,202,3	script	Моряк Офтамара#alberta	100,{
	mes "[Моряк Офтамара]";
	mes "Привет всем!";
	mes "За 10,000 Зени отправимся в Порт Малайя. Интересует?";
	next;
	switch(select("Расскажи о Порте Малайя.:Отправиться в Порт Малайя.:Отмена.")) {
	case 1:
		mes "[Моряк Офтамара]";
		mes "Порт Малайя, небольшая деревня к юго-западу от Альберты.";
		next;
		mes "[Моряк Офтамара]";
		mes "Я не смог посетить эту деревню, так как у них были какие то проблемы. Но сейчас вроде все решено";
		next;
		mes "[Моряк Офтамара]";
		mes "Я не знаю точно, но эта деревня, словно комфортный и уютный парк";
		next;
		mes "[Моряк Офтамара]";
		mes "Я был так близко, но мне нужно было возвращаться в Альберту";
		next;
		mes "[Моряк Офтамара]";
		mes "Я уверен, что вы будете себя хорошо чувствовать, когда достигните Порт Малайя";
		close;
	case 2:
		if (Zeny < 10000) {
			mes "[Моряк Офтамара]";
			mes "О Боже!!";
			mes "Вы не представляете как это далеко от сюда. Поэтому то это и не бесплатно. пожалуйста, возвращайтесь с 10,000 Зени. Извините!";
			close;
		}
		mes "[Моряк Офтамара]";
		mes "Отлично!";
		mes "Курс на Порт Малайя!!";
		set Zeny, Zeny-10000;
		close2;
		warp "malaya",271,55;
		end;
	case 3:
		mes "[Моряк Офтамара]";
		mes "Авантюристы заняты в эти дни. Может вернетесь еще и мы сможем посетить Порт Малайя вместе ";
		close;
	}
}

malaya,276,55,4	script	Моряк Офтамара#malaya	100,{
	if (malaya_hi < 10) {
		mes "[Моряк Офтамара]";
		mes "Я думаю, деревня еще оживленная. Так что корабль до Альберты вы всегда найдете";
		next;
	} else if (malaya_hi < 20) {
		mes "[Моряк Офтамара]";
		mes "Я думаю, безымянный авантюрист смог занять умы сельчан. Хотите вернуться в Альберту";
		next;
	} else {
		mes "[Моряк Офтамара]";
		mes "Хотели бы вернуться в Альберту со мной?";
		next;
	}
	switch(select("Возвращаемся.:Не сейчас.")) {
	case 1:
		mes "[Моряк Офтамара]";
		mes "Отлично! Давайте теперь вернемся в Алберту!!";
		close2;
		warp "alberta",239,68;
		end;
	case 2:
		mes "[Моряк Офтамара]";
		mes "Все хорошо. Возвращайтесь сюда, если хотите вернуться в Альберту";
		close;
	}
}

// Generic Port Malaya NPCs
//============================================================
ma_in01,30,94,4	script	Трактирщица#ma	583,{
	if (malaya_hi < 10) {
		mes "[Трактирщица]";
		mes "Боже мой. Посетитель?";
		mes "Неудачно время. На данный момент деревня неорганизованная. Поэтому я закрыта. извините ";
		close;
	} else if (malaya_hi < 20) {
		mes "[Трактирщица]";
		mes "Жители говорят что вы авантюрист? Тогда мне стоит открыть свой двор благодаря вам. Спасибо";
		next;
	} else {
		mes "[Трактирщица]";
		mes "Добро пожаловать.";
		mes "Этот двор лучший в Порте Малайя. 'Хижина в большом городе'.";
		next;
	}
	mes "[Трактирщица]";
	mes "Приедете на отдых? Жилье будет стоить 5,000 Зени.";
	next;
	switch(select("Магазин.:Домик. - 5,000з:Отмена.")) {
	case 1:
		mes "[Трактирщица]";
		mes "Вы успешно сохранены. Увидимся в следующий раз.";
		savepoint "ma_in01",43,98;
		close;
	case 2:
		if (Zeny < 5000) {
			mes "[Трактирщица]";
			mes "Извините. Я думала вы задержитесь подольше.";
			close;
		}
		mes "[Трактирщица]";
		mes "Надеюсь вам понравится пребывание тут.";
		close2;
		set Zeny, Zeny-5000;
		percentheal 100,100;
		warp "ma_in01",43,98;
		end;
	case 3:
		close;
	}
}

-	script	Unidentified Creature#i	-1,{
	end;
OnInit:
	switch(atoi(strnpcinfo(2))) {
		case 1: setarray .@npc$,"01","02","03","04"; break;
		case 5: setarray .@npc$,"05","06","07","08"; break;
		case 9: setarray .@npc$,"09","10","11"; break;
		default: end;
	}
	set .@size, getarraysize(.@npc$);
	set .@rand, rand(.@size);
	for(set .@i,0; .@i<.@size; set .@i,.@i+1)
		donpcevent "Unidentified Creature#"+.@npc$[.@i]+"::"+((.@i == .@rand)?"OnEnable":"OnDisable");
	end;
OnEnable:
	enablenpc strnpcinfo(0);
	end;
OnDisable:
	disablenpc strnpcinfo(0);
	end;
OnBingx2:
	switch(atoi(strnpcinfo(2))) {
		case 1: setarray .@npc$,"02","03","04"; break;
		case 5: setarray .@npc$,"06","07","08"; break;
		case 9: setarray .@npc$,"10","11"; break;
	}
	set .@size, getarraysize(.@npc$);
	set .@rand, rand(.@size);
	for(set .@i,0; .@i<.@size; set .@i,.@i+1)
		donpcevent "Unidentified Creature#"+.@npc$[.@i]+"::"+((.@i == .@rand)?"OnEnable":"OnDisable");
	donpcevent strnpcinfo(0)+"::OnDisable";
	end;
OnTouch_:
	if (getcharid(1)) {
		if ($ma_name04$ == strcharinfo(0) || $ma_name05$ == strcharinfo(0) || $ma_name06$ == strcharinfo(0)) {
			donpcevent strnpcinfo(0)+"::OnBingx2";
			mes "The unidentified creature gets a glimpse of you, blushes and then disappears.";
			close;
		}
		if (rand(1,5)%2) {
			switch(atoi(strnpcinfo(2))) {
				case 1: set $ma_name04$,strcharinfo(0); break;
				case 5: set $ma_name05$,strcharinfo(0); break;
				case 9: set $ma_name06$,strcharinfo(0); break;
			}
		}
		donpcevent strnpcinfo(0)+"::OnBingx2";
		mes "The unidentified creature gets a glimpse of you and disappears.";
		close;
	}
	donpcevent strnpcinfo(0)+"::OnBingx2";
	mes "The unidentified creature looks at you and runs away.";
	close;
}
ma_fild01,74,367,6	duplicate(Unidentified Creature#i)	Unidentified Creature#01	572,2,2
ma_fild02,282,41,4	duplicate(Unidentified Creature#i)	Unidentified Creature#05	572,2,2
ma_scene01,195,92,4	duplicate(Unidentified Creature#i)	Unidentified Creature#09	572,2,2

-	script	Unidentified Creature#j	-1,{
	end;
OnEnable:
	enablenpc strnpcinfo(0);
	end;
OnDisable:
	disablenpc strnpcinfo(0);
	end;
OnBingx2:
	switch(atoi(strnpcinfo(2))) {
		case 2: setarray .@npc$,"01","03","04"; break;
		case 3: setarray .@npc$,"01","02","04"; break;
		case 4: setarray .@npc$,"01","02","03"; break;
		case 6: setarray .@npc$,"05","07","08"; break;
		case 7: setarray .@npc$,"05","06","08"; break;
		case 8: setarray .@npc$,"05","06","07"; break;
		case 10: setarray .@npc$,"09","11"; break;
		case 11: setarray .@npc$,"09","10"; break;
	}
	set .@size, getarraysize(.@npc$);
	set .@rand, rand(.@size);
	for(set .@i,0; .@i<.@size; set .@i,.@i+1)
		donpcevent "Unidentified Creature#"+.@npc$[.@i]+"::"+((.@i == .@rand)?"OnEnable":"OnDisable");
	donpcevent strnpcinfo(0)+"::OnDisable";
	end;
OnTouch_:
	set .@i, atoi(strnpcinfo(2));
	if (.@i%4 == 2) {
		if (getcharid(0) == getpartyleader(getcharid(1),2))
			set .@pass,1;
	} else if (.@i%4 == 3) {
		if (checkquest(4229,PLAYTIME) > -1 && checkquest(9223,PLAYTIME) > -1 && checkquest(12278,PLAYTIME) > -1)
			set .@pass,1;
	} else if (.@i%4 == 0) {
		if (rand(1,100)%10 == 7)
			set .@pass,1;
	}
	if (.@pass) {
		if ($ma_name04$ == strcharinfo(0) || $ma_name05$ == strcharinfo(0) || $ma_name06$ == strcharinfo(0)) {
			donpcevent strnpcinfo(0)+"::OnBingx2";
			mes "The unidentified creature gets a glimpse of you, blushes and then disappears.";
			close;
		}
		if (rand(1,5)%2) {
			switch(.@i) {
			case 2:
			case 3:
			case 4:
				set $ma_name04$,strcharinfo(0);
				break;
			case 6:
			case 7:
			case 8:
				set $ma_name05$,strcharinfo(0);
				break;
			case 10:
			case 11:
				set $ma_name06$,strcharinfo(0);
				break;
			}
		}
		donpcevent strnpcinfo(0)+"::OnBingx2";
		mes "The unidentified creature gets a glimpse of you and disappears.";
		close;
	}
	donpcevent strnpcinfo(0)+"::OnBingx2";
	mes "The unidentified creature looks at you and runs away.";
	close;
}
ma_fild01,109,116,4	duplicate(Unidentified Creature#j)	Unidentified Creature#02	572,2,2
ma_fild01,280,150,6	duplicate(Unidentified Creature#j)	Unidentified Creature#03	572,2,2
ma_fild01,309,221,6	duplicate(Unidentified Creature#j)	Unidentified Creature#04	572,2,2
ma_fild02,246,324,4	duplicate(Unidentified Creature#j)	Unidentified Creature#06	572,2,2
ma_fild02,71,296,6	duplicate(Unidentified Creature#j)	Unidentified Creature#07	572,2,2
ma_fild02,32,263,4	duplicate(Unidentified Creature#j)	Unidentified Creature#08	572,2,2
ma_scene01,158,139,4	duplicate(Unidentified Creature#j)	Unidentified Creature#10	572,2,2
ma_scene01,167,112,6	duplicate(Unidentified Creature#j)	Unidentified Creature#11	572,2,2

malaya,227,311,4	script	Бабушка#ma01	575,{
	if (malaya_hi < 10) {
		mes "[Бабушка]";
		mes "Ты не здешний? Береги себя";
		next;
		mes "[Бабушка]";
		mes "Я беспокоюсь, потому что моя невестка беременна. Я надеюсь, ничего не случится.";
		close;
	} else if (malaya_hi < 20) {
		mes "[Бабушка]";
		mes "Я слышала что появился новичок, который помогает деревне.";
		next;
		mes "[Бабушка]";
		mes "Потом моя невестка будет в безопасности.";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[Бабушка]";
		mes "Слышала что там есть "+getd("$ma_name0"+rand(1,6)+"$")+" преследуют Мангукала.";
		next;
		mes "[Бабушка]";
		mes "Я буду переживать.";
		close;
	}
	mes "[Бабушка]";
	mes "Я беспокоюсь за свою беременную дочку. надеюсь Мангукала не будет её беспокоить.";
	next;
	select("Мангукала?");
	mes "[Бабушка]";
	mes "Не здешний? Позвольте мне рассказать вам, почему даже иностранец, как вы должны быть осторожны.";
	next;
	mes "[Бабушка]";
	mes "Мангукала была женщиной, которая жила в Порт Малайе давно. Она вышла замуж за достойного человека, и был у них ребенок.";
	next;
	mes "[Бабушка]";
	mes "Но счастье никогда не длится вечно, не так ли? Ребенок был выкидышам от несчастного случая...";
	next;
	mes "[Бабушка]";
	mes "В конце концов она умерла после того, как оплакивала потерянного ребенка в течение нескольких дней.";
	next;
	select("Это печальная история.");
	mes "[Бабушка]";
	mes "Затем начали появляться в духи и стали забирать детей беременных женщин в деревне.";
	next;
	mes "[Бабушка]";
	mes "Если вы когда-нибудь гуляли по ночному Порту Малайи...";
	next;
	mes "[Бабушка]";
	mes "И если вы услышите странный звук, будьте осторожны, возможно это звук приближающаяся Мангукала.";
	close;
}

malaya,189,263,4	script	Барабанящий молодой человек#ma02	578,{
	if (malaya_hi < 10) {
		emotion e_omg;
		mes "[Молодой человек]";
		mes "Тьфу... это просто как в тот раз, прежде чем...";
		next;
		emotion e_omg;
		mes "[Молодой человек]";
		mes "Когда луна поглотили";
		close;
	} else if (malaya_hi < 20) {
		mes "[Молодой человек]";
		mes "Я думаю, деревня была спасена безымянным авантюристом.";
		next;
		emotion e_sigh;
		mes "[Молодой человек]";
		mes "Фух... я думал Баконава снова появился.";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[Молодой человек]";
		mes getd("$ma_name0"+rand(1,6)+"$")+" говорят что остановили Баконава поглотить луну. разве это не потрясающе? Я так рад.";
		close;
	}
	mes "[Молодой человек]";
	mes "Баконава также известен как 'монстр, который глотает луну' в Порту Малайя.";
	next;
	mes "[Молодой человек]";
	mes "Но я так же знаю что Баконава может проглотить и людей.";
	next;
	select("Хух! Разве это не опасно?");
	emotion e_gg;
	mes "[Молодой человек]";
	mes "Ха ха ха. Есть способ помешать Баконава поглощать людей.";
	next;
	mes "[Молодой человек]";
	mes "Да, есть способ, благодаря которому можно не позволить проглотить ему луну в шестой раз.";
	next;
	select("Вау. И что же это?");
	mes "[Молодой человек]";
	mes "Он очень чувствителен к шуму, когда хочет совершить свое нападение!!";
	next;
	mes "[Молодой человек]";
	mes "Это ваш шанс!! не имеет значение каким будет шум!! Самое главное просто продолжать шуметь и отвлекать его.";
	next;
	mes "[Молодой человек]";
	mes "Тогда Баконава будет удивлен и не сможет сделать задуманного!";
	next;
	mes "[Молодой человек]";
	mes "Возьмите барабан с собой и держите его под рукой.";
	close;
}

malaya,270,59,4	script	Портовый охранник#ma03	570,{
	if (malaya_hi < 10) {
		emotion e_swt2;
		mes "[Портовый охранник]";
		mes "В деревне шумно в эти дни. это хорошо для меня. Люблю такое.";
		close;
	} else if (malaya_hi < 20) {
		emotion e_no;
		mes "[Портовый охранник]";
		mes "В деревне вроде как спокойно, но все равно вы должны быть аккуратны, гуляя по ночам";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[Портовый охранник]";
		mes getd("$ma_name0"+rand(1,6)+"$")+" Говорят что бы уйти в живых от Бувая, человек должен быть очень сильным.";
		close;
	}
	emotion e_omg;
	mes "[Портовый охранник]";
	mes "Что!! В порту все чисто!!";
	next;
	mes "[Портовый охранник]";
	mes "Хах? Вы не капитан? Вы меня напугали. Я получил официальный документ для путешественников.";
	next;
	select("Что за документ?");
	mes "[Портовый охранник]";
	mes "в нем сказано, что путешественники должны быть осторожны гуляя по деревне в ночное время.";
	next;
	mes "[Портовый охранник]";
	mes "Бувай не так часто появляется в деревне. Но были случаи когда он нападал в ночное время.";
	next;
	mes "[Портовый охранник]";
	mes "Ах да?! Еще тут сказано что монстр по имени Вакавака очень часто посещает деревню. Поэтому нужно быть очень острожным.";
	next;
	switch(select("Понял.:Есть ли способ, чтобы избежать их?")) {
	case 1:
		mes "[Портовый охранник]";
		mes "Будьте осторожны в ночное время!";
		close;
	case 2:
		mes "[Портовый охранник]";
		mes "Еще говорят что у Бувая есть слабое место. Коробка, которую он носит. Лучше атаковать туда, если окажитесь в ловушке.";
		next;
		mes "[Портовый охранник]";
		mes "И лучше бежать в ближайший дом, если услышите приближение Вакавака.";
		next;
		emotion e_hmm;
		mes "[Портовый охранник]";
		mes "Но если вы ничего не услышали и оказались в ловушке, то будьте готовы защищаться.";
		close;
	}
}

malaya,88,252,4	script	Маленькая девочка#ma04	576,{
	if (malaya_hi < 10) {
		mes "[Маленькая девочка]";
		mes "Я боюсь, но мне нужно посетить фею, которая обитает в лесу";
		close;
	} else if (malaya_hi < 20) {
		mes "[Маленькая девочка]";
		mes "Отец сказал, что я смогу посетить её, когда в деревне будет все спокойно.";
		close;
	}
	if (rand(1,3) == 2) {
		set .@name$, getd("$ma_name0"+rand(1,6)+"$");
		mes "[Маленькая девочка]";
		mes "недавно "+.@name$+" пришел ко мне и рассказал забавную историю.";
		next;
		mes "[Маленькая девочка]";
		mes "'Я посадила пару груш во дворе'";
		mes "разве это не весело? "+.@name$+" кажется он забавный.";
		close;
	}
	mes "[Маленькая девочка]";
	mes "Отец сказал что я не буду болеть, так как дружу с феей";
	next;
	select("Что за фея?");
	mes "[Маленькая девочка]";
	mes "Энканто! Феи Энканто живут в лесу. И между прочим есть мальчики и девочки феи...";
	next;
	mes "[Маленькая девочка]";
	mes "И кстати мальчики красивее. Почему так?";
	close;
}

malaya,219,92,6	script	Little Kid#ma05	577,{
	if (malaya_hi < 10) {
		mes "[Little Kid]";
		mes "Моя мама сказала мне, чтобы не играл на улице, потому что тут опасно, почему.?";
		close;
	} else if (malaya_hi < 20) {
		mes "[Little Kid]";
		mes "Моя мама сказала мне, что я могу играть, но только в Порт Малайе.";
		next;
		mes "[Little Kid]";
		mes "Но я никогда не думал о том чтобы выходить за пределы порта Малайи.";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[Little Kid]";
		mes "Я слышал что кто-то взял у шляпу у Жежелинга в таинственной деревне";
		next;
		mes "[Little Kid]";
		mes "Кто бы мог украсть шляпу у монстра? "+getd("$ma_name0"+rand(1,6)+"$")+" видимо он отчаянный.";
		next;
		mes "[Little Kid]";
		mes "Ой! Это должно было быть секретом. Пожалуйста, сделайте вид будто не слышали меня.";
		close;
	}
	mes "[Little Kid]";
	mes "Я думаю что кто то крадет все шляпы в деревне.";
	next;
	mes "[Little Kid]";
	mes "Шляпка моей мамы пропала. А несколько дней назад пропала шляпа нашего соседа.";
	next;
	mes "[Little Kid]";
	mes "Хмм... Как вы думаете, может это делают Жежелинги?";
	next;
	mes "[Little Kid]";
	mes "Когда я был у друга в таинственной деревне, он рассказал что видел эти шляпы у Жежелингов...";
	close;
}

malaya,363,283,4	script	Местный житель#ma06	582,{
	if (malaya_hi < 10) {
		mes "[Местный житель]";
		mes "Хмм... будьте осторожны с проклятием ведьм?";
		next;
		mes "[Местный житель]";
		mes "Еще остерегайтесь игл Мангукалы, иностранец.";
		close;
	} else if (malaya_hi < 20) {
		mes "[Местный житель]";
		mes "Добро пожаловать в Порт Малайя, иностранец..";
		next;
		mes "[Местный житель]";
		mes "В деревне шумно в эти дни. Если увидите кого-то с красными глазами, стараюсь не смотреть в глаза.";
		close;
	}
	set .@name$, getd("$ma_name0"+rand(1,6)+"$");
	switch(rand(1,6)) {
	case 1:
		mes "[Местный житель]";
		mes "Будьте осторожны, гуляя по деревне ночью. "+.@name$+" может быть снесен Вакавака.";
		close;
	case 2:
		mes "[Местный житель]";
		mes .@name$+" говорили что успешно приручили Тикбалана. видимо это человек очень мужественный. Интересно, что мне нужно чтобы увидеть прирученного Тикбалана?";
		close;
	case 3:
		mes "[Местный житель]";
		mes "Вы когда нибудь видели Бангиса? Я слышшал "+.@name$+" охотятся на Бангиса.";
		close;
	case 4:
		mes "[Местный житель]";
		mes "Если вы планируете выйти за пределы порта, опасайтесь Тиюнака. "+.@name$+" говорят что после встречи с Тиюнаком, приходилоь ехать в больницу.";
		close;
	case 5:
	case 6:
		break;
	}
	mes "[Местный житель]";
	mes "В деревне шумно в эти дни. Говорят что Мангукала вновь вернулась.";
	next;
	select("Мангукала?");
	mes "[Местный житель]";
	mes "Монстр, которого еще называют ведьмя. Она носит одежду как люди.";
	next;
	mes "[Местный житель]";
	mes "Мангукала делает из людей куклу и проклинает их иглами.";
	next;
	select("Есть ли способ распознать этого монстра?");
	mes "[Местный житель]";
	mes "Есть один способ. У всех Мангукал красные глаза.";
	next;
	mes "[Местный житель]";
	mes "Вы легко можете их распознать! но нужно помнить одну вещь.";
	next;
	mes "[Местный житель]";
	mes "Вы можете быть зачарованы мангукалой, если будете долго смотреть в их глаза.";
	next;
	mes "[Местный житель]";
	mes "Никогда не смотрите в их глаза слишком долго.";
	close;
}

malaya,41,127,6	script	Старик #ma07	574,{
	if (malaya_hi < 10) {
		mes "[Старик]";
		mes "Чужеземцам не очень рады в такие суетливые дни как сейчас.";
		close;
	} else if (malaya_hi < 20) {
		mes "[Старик]";
		mes "Вам не повезло попасть в такое время. даже толком не поприветствовали";
		close;
	}
	if (rand(1,3) == 2) {
		mes "[Старик]";
		mes getd("$ma_name0"+rand(1,6)+"$")+" сказал выкинуть Желлопи в деревне.";
		next;
		mes "[Старик]";
		mes "Такс, такс... Должно быть человек не достоин Желлопи.";
		close;
	}
	mes "[Старик]";
	mes "Прежде чем срезать дерево тебе необходимо убедиться в том, что Бангугот не живет в этом дереве.";
	next;
	mes "[Старик]";
	mes "Потому что происходили странные вещи с досками в которых жили Бангуготы.";
	next;
	select("Что произойдет?");
	mes "[Старик]";
	mes "Для ангуготов очень болезненно, когда их дома срубают, и они приходят в ярость.";
	next;
	mes "[Старик]";
	mes "Если ты захочешь отдохнуть у этих деревьев, они не дадут тебе спать и будут устраивать всякие шалости.";
	next;
	mes "[Старик]";
	mes "со временем, они могут стать куда злее и будут душить тебя пока ты не задохнешься.";
	next;
	select("О, боже...");
	mes "[Старик]";
	mes "Бугагага. так что лучше не отдыхать рядом с деревьями в которых живут Бангуготы.";
	close;
}

malaya,63,185,4	script	Местная жительница#ma08	583,{
	if (malaya_hi < 10) {
		mes "[Местная жительница]";
		mes "мне нечем с тобой поделиться.";
		close;
	} else if (malaya_hi < 20) {
		mes "[Местная жительница]";
		mes "Я волнуюсь за детей. Надеюсь они не напуганы происходящем в деревне.";
		close;
	}
	if (rand(1,3) == 2) {
		set .@name$, getd("$ma_name0"+rand(1,6)+"$");
		set .@rand, rand(1,10);
		if (.@rand < 5) {
			mes "[Местная жительница]";
			mes "Я люблю веселые истории. Не так давно, "+.@name$+" пришел и рассказал мне историю.";
			next;
		}
		switch(.@rand) {
		case 1:
			mes "[Местная жительница]";
			mes "'моя тетя наступила на муравья.'";
			next;
			break;
		case 2:
			mes "[Местная жительница]";
			mes "'Ты ешь соус чили?'";
			next;
			break;
		case 3:
			mes "[Местная жительница]";
			mes "'Почему ты наносишь муку на этот цветок?'";
			next;
			break;
		case 4:
			mes "[Местная жительница]";
			mes "В оружейном магазине, 'Я куплю иглы и нитки, а ты купи сено для того что бы спрятать их там.'";
			next;
			mes "[Местная жительница]";
			mes .@name$+" сказал это и его вышвырнули из магазина.";
			next;
			break;
		case 5:
			mes "[Местная жительница]";
			mes "Сотрудница Кафры с которой я дружу, рассказала мне мне эту историю про клиента.";
			next;
			mes "[Местная жительница]";
			mes "'Кафры мудрые, и поэтому никогда не выходят замуж, но если уж вышли, то они совсем не мудрые.'";
			next;
			mes "[Местная жительница]";
			mes "Ха ха... Мне нравятся смешные истории, которые немного трудноваты для меня.";
			close;
		default:
			mes "[Местная жительница]";
			mes "мне нравятся веселые истории. О, кстати! "+.@name$+", Любовь фотогенична. Ты согласен?";
			next;
			mes "[Местная жительница]";
			mes "Необхадима тьма чтобы развиваться.";
			next;
		}
		mes "[Местная жительница]";
		mes "Ха ха. неужели это не смешно?";
		close;
	}
	mes "[Местная жительница]";
	mes "Если ты услышишь плачь ребенка, никогда не подходи туда.";
	next;
	select("Почему?");
	mes "[Местная жительница]";
	mes "Есть монстр по имени Тиюнак который начинает плакать, после того как превращается в ребенка.";
	next;
	mes "[Местная жительница]";
	mes "Но когда путешественники поднимают его, он возвращается в первоначальный вид и атакует их.";
	next;
	select("Какой то подлый монстр.");
	mes "[Местная жительница]";
	mes "Да, этот монстр любит подшучивать над добрыми путешественниками, но...";
	next;
	mes "[Местная жительница]";
	mes "Есть легенда, что этот монстр произошел от души нерожденного ребенка. Как то это печально.";
	close;
}

// Jeepneys
//============================================================
function	script	F_Malaya_Jeepney	{
	.@mapName$        = getarg(0);
	.@passengers      = getarg(1);

	// set the other messages of varying amount)
	for (.@i = 5; .@i < getargcount(); .@i++) {
		setd(".@msgJeepneyInfo$[" + (.@i - 5) + "]", getarg(.@i));
	}

	if (malaya_hi < 10) {
		mes "[Джипни]";
		mes getarg(2);
		close;
	} else if ((malaya_hi >= 10) && (malaya_hi < 20)) {
		mes "[Джипни]";
		mes getarg(3);
		close;
	} else {
		mes "[Джипни]";
		mes getarg(4);
		next;
	}
	switch(select("Board [Passenger " + getmapusers(.@mapName$) + "/" + .@passengers +"]:Джипни?:Тут есть другие Джипни?:Ах... Да...")) {
	case 1:
		if (getmapusers(.@mapName$) >= .@passengers) {
			mes "[Джипни]";
			mes "Я боюсь но Джипни заполнена.";
			mes "Я сожалею, но что насчет других Джипни?";
			close;
		} else {
			mes "[Джипни]";
			mes "Удачного дня.";
			close2;
			warp .@mapName$,29,24;
			end;
		}
	case 2:
		// iterate through all the jeepney information for this specific NPC
		for (.@i = 0; .@i < getarraysize(.@msgJeepneyInfo$); .@i++) {
			mes "[Джипни]";
			mes getd(".@msgJeepneyInfo$[" + .@i + "]");

			if (.@i < (getarraysize(.@msgJeepneyInfo$) - 1)) {
				next;
			}
		}
		close;
	case 3:
		mes "[Джипни]";
		mes "ох! конечно же есть и другие Джипни.";
		next;
		mes "[Джипни]";
		mes "в Порте Малайя есть на 12, 30 и 60 персон Джпини, и все они конечно же работают.";
		next;
		mes "[Джипни]";
		mes "не желаете ли узнать месторасположение других джипни?";
		next;
		switch (select("Я хочу узнать расположение Джипни на 12 мест.:Я хочу узнать расположение Джипни на 30 мест.:Я хочу узнать расположение Джипни на 60 мест.")) {
		case 1:
			viewpoint 1, 237, 240, 1, 0xF7E009;
			viewpoint 1, 67 , 44 , 2, 0xF7E009;
			viewpoint 1, 282, 129, 3, 0xF7E009;
			set .@zif_in, 12;
			break;
		case 2:
			viewpoint 1, 134, 250, 4, 0xF7E009;
			viewpoint 1, 341, 153, 5, 0xF7E009;
			viewpoint 1, 293, 290, 6, 0xF7E009;
			set .@zif_in, 30;
			break;
		case 3:
			viewpoint 1, 242, 221, 7, 0xF7E009;
			viewpoint 1, 62 , 245, 8, 0xF7E009;
			viewpoint 1, 257, 58 , 9, 0xF7E009;
			set .@zif_in, 60;
			break;
		}
		mes "[Джипни]";
		mes .@zif_in+" Мы пометили расположение Джипни на карте.";
		next;
		break;
	}
	mes "[Джипни]";
	mes "Удачного пути.";
	close;
}

malaya,237,240,4	script	Джипни#01	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif01",
		12,

		// Messages for malaya_hi checks.
		"Ох.... Я боюсь... таааак боюсь... что случилось с этим местом?",
		"тут по прежнему хаос... Я удивлюсь, если смогу еще управлять джипни...",
		"ха ха ха добро пожаловать. Меня зовут ^1561EЭволь^000000, я управляю 12 местным Джиппи.",

		// Monologues of variable lengths.
		"Это ваш первый визит в Порт Малайя?",
		"Мы стараемся разрекламировать наши Джипни. Так что я вам все расскажу.",
		"Сначала джипни использовался как грузовой транспорт, но в последующем мы переделали их под перевозку людей.",
		"Мы учли что у всех разные вкусы и мы постарались учесть это в нашем оформлении.",
		"Конечно ты не можешь ими управлять, но никто не запрещает воспользоваться нашими услугами."
	);
}

malaya,67,44,4	script	Джипни#02	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif02",
		12,

		"О, что происходит в этой больнице...",
		"мне кажется что в больнице стало меньше стонов...",
		"Я ^1561EAChui^000000, управляю 12 местным джиппи. Привет~",

		"Это ваш первый визит в Порт Малайя?",
		"Мы стараемся разрекламировать наши Джипни. Так что я вам все расскажу.",
		"Сначала джипни использовался как грузовой транспорт, но в последующем мы переделали их под перевозку людей.",
		"я сам переделал свой джипни...",
		"Для вашей же безопасности у меня установлен лимит на количество пассажиров, но это полностью безопасно. Будь спокоен..."
	);
}

malaya,282,129,4	script	Джипни#03	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif03",
		12,

		"Это нехорошо для чужеземцев... Прежде чем общаться с местными, вам бы следовало пообщаться с Мумбаки Фон.",
		"ты тот путешественник который встретился с Мумбаки Фон? нет, стой... тогда ты не должен быть тут... да это призрак... Ааааааа...",
		"меня зовут ^1561EATop^000000, один из трех водитель 12 местной джипни .",

		"Первый раз в деревне?",
		"Мы стараемся разрекламировать наши Джипни. Так что я вам все расскажу.",
		"Сначала джипни использовался как грузовой транспорт, но в последующем мы переделали их под перевозку людей.",
		"они совершенно разные, все зависит от водителя...",
		"Теперь это достопримечательность Порта Малайи."
	);
}

malaya,134,250,6	script	Джипни#04	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif04",
		30,

		"У...у... уходи отсюда...",
		"Аррр... ты спугнул дьявола во мне... Уходи прочь... Кыш...",
		"^1561EABrav^000000, водитель 30 местной джипни к вашим услугам, вожу так безопасно как могу, с учетом того что я очень пуглив.",

		"Это ваш первый визит в Порт Малайя?",
		"Сначала джипни использовался как грузовой транспорт, но в последующем мы переделали их под перевозку людей.",
		"Мы усовершенствовали транспорт и украсили их",
		"Если вы посещаете Порт Малайя, вы неприменно должны прокатиться на джипни.",
		"Бугагага, конечно ты не можешь их водить."
	);
}

malaya,341,153,4	script	Джипни#05	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif05",
		30,

		"Ппп... Призраки в деревне... Эй... В тебе есть призрак?",
		"Я думаю в тебе меньше призраков... Теперь в деревне стало тише...",
		"Привет, я ^1561EALivil^000000 водитель 30 местной джипни. безопасность, мой конек.",

		"Первый раз в деревне?",
		"Я буду твоим гидом и дам тебе парочку советов.",
		"Сначала джипни использовался как грузовой транспорт, но в последующем мы переделали их под перевозку людей.",
		"Для безопасности джипни ограничены по числу пассажиров.",
		"Раньше перевозили вплоть до 180 человек, но теперь в Порте Малайи таких нет."
	);
}

malaya,293,290,6	script	Джипни#06	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif06",
		30,

		"Ссс... Стражники... Что же делают стражники...",
		"Неужели стражники начали действовать? Фух... Что за спешка...",
		"Я ^1561EAGad^000000, водитель 30 местной джипни.",

		"Я вижу ты первый раз в деревне.",
		"Быть гидом, мое хобби. Дай мне рассказать.",
		"Джипни были хорошим грузовым транспортом, но теперь мы усовершенствовали их с помощью новых технологий Порта Малайи!!",
		"Этот джипни ручной работы. Хахаха... Это круто, не так ли? Хахаха",
		"Для нас каждый клиент, король, Но даже так я не могу позволить сесть за руль... Бугагага"
	);
}

malaya,242,221,4	script	Джипни#07	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif07",
		60,

		"о нет... я думаю вся деревня заколдована призраками... Может быть... Может мне стоит посетить лидера Мумбаки...",
		"Призраки... Я уверен что лидер Мумбаки знает как избавиться от них...",
		"^1561EAHott^000000, к вашим услугам оператор 60 местного джипни.",

		"Джипни, публичный транспорт порта Малайи.",
		"они повсюду. Они бесплатны! у всех разныое количество пассажиров, но это не проблема, они повсюду."
	);
}

malaya,62,245,6	script	Джипни#08	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif08",
		60,

		"Кыш~ Если ты призрак, убирайся прочь, и даже если ты человек, убирайся~ Кыш~",
		"Эй... Ты... Ты ведь человек? не так ли? Аа? Скажи что ты человек... Пожалуйста...",
		"Хелло, Я ^1561EAMuyan^000000, к вашим услугам оператор 60 местного джипни. Приятно познакомиться.",

		"Джипни, публичный транспорт, безопасность самое главное.",
		"Мой джипни стал самым лучшим в Порте Малайи, '60 местный Джипни' сертификат.",
		"Это значит что он идеален и безопасен. Так что можешь не волноваться о своей безопасности во время поездки."
	);
}

malaya,257,58,6	script	Джипни#09	582,{
	callfunc(
		"F_Malaya_Jeepney",
		"ma_zif09",
		60,

		"...Если ты чужеземец... ничего не  трогай и будь осторожным в этой деревне...",
		"Эта деревня все еще опасна. лучше ничего не трогать до тех пор пока не убедишься что это безопасно.",
		"Привет. Я ^1561EAGramma^000000, к вашим услугам оператор 60 местного джипни. Приятно познакомиться.",

		"Это твой первый раз в Порте малайя?",
		"Мы стараемся разрекламировать наши Джипни. Так что я вам все расскажу.",
		"Сначала джипни использовался как грузовой транспорт, но в последующем мы переделали их под перевозку людей.",
		"Мы учли что у всех разные вкусы и мы постарались учесть это в нашем оформлении.",
		"Конечно ты не можешь ими управлять, но никто не запрещает воспользоваться нашими услугами."
	);
}
