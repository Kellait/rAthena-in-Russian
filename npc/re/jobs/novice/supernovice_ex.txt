//===== rAthena Script ======================================= 
//= Expanded Super Novice Job Quest
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1a
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Job Change quest for Expanded Super Novice class.
//===== Additional Comments: ================================= 
//= 1.0 All dialogue is custom, contains some incorrect behaviors.
//= 1.1 Switched to a translated copy of the official script
//=     by Gennosuke Kouga, but vastly edited its content. [Euphy]
//= 1.1a Replaced effect numerics with constants. [Euphy]
//============================================================ 
//= Translated en -> ru [Akaineko] 
//============================================================

function	script	Esseray_Ex	{
	if (BaseLevel > 98 && JobLevel > 98) {
		if (EXSUPNOV_Q > 0 && EXSUPNOV_Q < 6) {
			mes "[Эссерэй]";
			mes "Легендарный Новичок ушёл по какому-то делу в район Комодо. Тебе стоит найти его.";
			next;
			mes "[Эссерэй]";
			mes "Он проведёт тебя по правильному пути...";
			close;
		}
		mes "[Эссерэй]";
		mes "О? "+(Sex?"Брат":"Сестрица")+"-Новичок? Неплохо выглядишь.";
		next;
		mes "[Эссерэй]";
		mes "Ты в расцвете сил... Похоже, их у тебя достаточно, чтобы пройти тест.";
		next;
		mes "[Эссерэй]";
		mes "Я знаю способ стать сильнее. Хочешь попробовать?";
		next;
		if(select("Нет, я довол"+(Sex?"ен":"ьна")+" тем, что есть.:Я хочу стать сильнее!") == 1) {
			mes "[Эссерэй]";
			mes "Посредственность это славно.";
			mes "Живи счастливо.";
			close;
		}
		mes "[Эссерэй]";
		mes "Отлично!";
		mes "Легенда мира Новичков ушёл по какому-то делу в Комодо. Тебе стоит найти его.";
		next;
		setquest 5092;
		set EXSUPNOV_Q,1;
		mes "[Эссерэй]";
		mes "Он поможет тебе пройти по правильному пути...";
		close;
	}
	else return;
}

beach_dun2,119,92,4	script	Fishing Novice#sp2	567,{
	if (checkquest(5092) >= 0) {
		mes "[Рыбачащий новичок]";
		mes "Тсс --------- !!";
		mes "Не топай.";
		mes "Рыбу распугаешь.";
		next;
		select("Хмм.. Извини...");
		mes "[Рыбачащий новичок]";
		mes "Аааак!!";
		mes "Ты что творишь?";
		mes "Говорил же не шуметь!";
		mes "Они уплывают, когда слышат нас~";
		next;
		mes "[Рыбачащий новичок]";
		mes "(-- трясёт головой --)";
		mes "Ты... кто вообще?";
		mes "Чего рыбачить мешаешь?";
		next;
		select("Я прин"+(Sex?"ёс":"есла")+" кое-что от Эссерэя...");
		emotion e_hmm;
		mes "[Рыбачащий новичок]";
		mes "Чего? Эссерэй? Эссерэй?";
		mes "Хмм! Я его знаю?..";
		mes "Эссерэй... Эссерэй... хмм.";
		next;
		emotion e_dots,1;
		emotion e_dots;
		mes "..................";
		next;
		emotion e_gasp;
		mes "[Рыбачащий новичок]";
		mes "Ахххх --- !!";
		mes "Вспомнил! --- Вспомнил!";
		mes "^0000FFЧлен Общества Новичков в Альдебаране.^000000";
		mes "Эссерэй, да? Нет?";
		next;
		if(select("Нет:Угадал") == 2) {
			mes "[Рыбачащий новичок]";
			mes "Ху ху~ Интуиция всё ещё со мной...";
			next;
			mes "[Рыбачащий новичок]";
			mes "Так чего тебе от меня надо?";
		}
		else {
			mes "[Рыбачащий новичок]";
			mes "Нет?! Не он?";
			mes "Что это за Эссерэй тогда??";
			next;
			emotion e_dots,1;
			emotion e_dots;
			mes ".... .... .... ....";
			next;
			mes "[Рыбачащий новичок]";
			mes "Что -------- !!";
			mes "Это не важно, правильно?";
			next;
			mes "[Рыбачащий новичок]";
			mes "Ну так -------- !!";
			mes "Что тебе здесь нужно?";
		}
		next;
		mes "- Вы рассказываете ему свою историю. -";
		next;
		emotion e_hmm;
		mes "[Рыбачащий новичок]";
		mes "Хмм... Этот вопрос одновременно и сложен, и прост.";
		next;
		mes "- Слышно какое-то обсуждение. -";
		next;
		emotion e_an;
		mes "[Рыбачащий новичок]";
		mes "Эссерэй! %@&$# сын, весь отпуск испортил...";
		next;
		mes "[Рыбачащий новичок]";
		mes "Аааахххх~";
		mes "Жизнь моя, жестянка~";
		mes "Аххх~ столько проблем.";
		next;
		emotion e_what;
		mes "[Рыбачащий новичок]";
		mes "Че~ересчур. Хэй, мелюзга! Как тебя зовут?";
		next;
		select(strcharinfo(0)+", а тебя?");
		mes "[Нодор]";
		mes "Меня зову Нодор. Нодор.";
		next;
		mes "[Нодор]";
		mes ".... .... .....";
		next;
		mes "[Нодор]";
		mes "Эй! У тебя на лице написано, о чём ты думаешь~";
		next;
		mes "[Нодор]";
		mes "Надо мной всю жизнь смеются. Кондор, Одор... Не глупи.";
		next;
		changequest 5092,5093;
		mes "[Нодор]";
		mes "Какова бы ни была причина... Ты здесь!";
		mes "Будем партнёрами. Подойди чуть попозже~";
		next;
		mes "- Поговорить с ним ещё раз. -";
		close;
	}
	else if (checkquest(5093) >= 0) {
		mes "[Нодор]";
		mes "Ээммммхммм ----- !!";
		next;
		mes "- Нодор с подозрением оглядывает вас. -";
		next;
		mes "[Нодор]";
		mes "Ты знал, что ^0000FFСупер-новичок^000000 был идеален?";
		next;
		mes "- Спрашивает Нодор доверительно. -";
		next;
		mes "[Нодор]";
		mes "Эй! Супер-новичок~";
		mes "Знаешь, кто я такой?";
		next;
		select("Ты о чём вообще?!");
		mes "[Нодор]";
		mes "Хех...";
		mes "Нервничаешь?";
		next;
		select("... ..... ...");
		mes "- Этот Нодор просто псих. -";
		next;
		mes "[Нодор]";
		mes "Эй! Супер-новичок ~";
		mes "Я сделаю так, чтобы ты стал"+(Sex?"":"а")+" сильнее!";
		mes "Ну что, будешь мне доверять?";
		next;
		if(select("Не желаю иметь с тобой дел.:Почему бы и нет? Кекеке~") == 1) {
			mes "[Нодор]";
			mes "Отказываешься? Ты разве не мечтал"+(Sex?"":"а")+"  стать сильнее? Я тоже обожаю баклуши бить. Если тебе ничего не надо, не мешай рыбачить.";
			close;
		}
		mes "[Нодор]";
		mes "Эммхмм~ Я разве грубил?";
		mes "Хмм, не могу промолчать.";
		next;
		mes "[Нодор]";
		mes "Не обращай внимания. Мне ничего не стоит научить тебя быть сильнее.";
		next;
		mes "[Нодор]";
		mes "Отлично~!";
		mes "В чём твоя проблема?";
		mes "Ты в упор не понимаешь, как дальше развиваться, я прав?";
		next;
		select("Да, не могу я просто.");
		emotion e_hmm;
		mes "[Нодор]";
		mes "Хмхмхмм ~!";
		mes "Есть один выход...";
		mes "Это опасно, но я тебе расскажу.";
		next;
		mes "[Нодор]";
		mes "Но при одном условии. Ты не задаёшь вопросов и делаешь всё, что я скажу!";
		mes "Ну что? Соглас"+(Sex?"ен":"на")+" ?";
		next;
		if(select("Нет!:Давай!") == 1) {
			mes "[Нодор]";
			mes "Быть сильным значит жить! Ты же не хочешь вечно быть соплёй?";
			next;
			mes "[Нодор]";
			mes "Я дам тебе подсказку. Ты не сможешь всегда оказываться прав"+(Sex?"":"а")+". Однажды придётся засунуть своё эго подальше и прислушаться к остальным.";
			emotion e_an;
			close;
		}
		mes "[Нодор]";
		mes "Ладно --------- !!";
		mes "Страсть сделает тебя сильнее. Ты споко"+(Sex?"ен":"йна")+"?";
		next;
		mes "[Нодор]";
		mes "Тебе нужно сделать вот что...";
		mes "Убей 1000 монстров.";
		next;
		mes "[Нодор]";
		mes "Знаешь, для чего это?";
		next;
		setarray .@Monsters$[0],"Поринг","Лунатик","Фабре","Пикки";
		set .@i, rand(4);
		changequest 5093,5094+.@i;
		mes "[Нодор]";
		mes "Знаешь, что за монстр такой: ^FF0000"+.@Monsters$[.@i]+"^000000?";
		next;
		mes "[Нодор]";
		mes "Убей тысячу! Не говори, что тебе слабо! Да, это дофига, но они же мелочь!";
		next;
		select("Ты шутишь?");
		mes "[Нодор]";
		mes "Хах хах~!";
		mes "Помнишь, без вопросов!";
		mes "Не приходи, пока не набьёшь тысячи.";
		close;
	}
	else if (checkquest(5094) >= 0 || checkquest(5095) >= 0 || checkquest(5096) >= 0 || checkquest(5097) >= 0) {
		mes "["+strcharinfo(0)+"]";
		mes "Эй, Нодор!";
		next;
		emotion e_omg;
		mes "[Нодор]";
		mes "Боже! Чего пугаешь... Чего тебе?";
		next;
		if (checkquest(5094,HUNTING) != 2 && checkquest(5095,HUNTING) != 2 && checkquest(5096,HUNTING) != 2 && checkquest(5097,HUNTING) != 2) {
			mes "[Нодор]";
			mes "Эй! Супер-новичок!";
			mes "Я разве не говорил убить тысячу? Не возвращайся, пока не закончишь.";
			close;
		}
		mes "[Нодор]";
		mes "Побил"+(Sex?"":"а")+" ровно 1000?";
		next;
		mes "[Нодор]";
		mes "Ты круче чем я думал. Короче, на кого я тебе там говорил охотиться?";
		next;
		set .@i, select("Поринг:Лунатик:Фабре:Пикки:Бафомет")-1;
		if (.@i == 4) {
			mes "[Нодор]";
			mes "Хехех? Ты правда ходил"+(Sex?"":"а")+" на Бафомета в одиночку?..";
			mes "Не ври мне, а то получишь!!";
			close;
		}
		if (checkquest(5094+.@i,HUNTING) != 2) {
			mes "[Нодор]";
			mes "Не ври! Я не говорил такого!";
			close;
		}
		changequest 5094+.@i,5098;
		emotion e_heh;
		mes "[Нодор]";
		mes "Кехехехехехе~";
		next;
		mes "[Нодор]";
		mes "Ты меня так задолбал"+(Sex?"":"а")+", что я придумал такое задание...";
		next;
		mes "[Нодор]";
		mes "Что, правда убил"+(Sex?"":"а")+" тысячу? Ну, без вопросов, страсти тебе не занимать.";
		next;
		mes "[Нодор]";
		mes "Окей. Пойдёт. Так уж и быть, расскажу тебе, как преодолеть пределы своих сил.";
		close;
	}
	else if (checkquest(5098) >= 0) {
		if (rand(10) != 1) {
			mes "[Нодор]";
			mes "Ой! Ты опять?";
			mes "Разве у тебя не были какие-то дела? Страсти у тебя дофига... А вот терпению бы ещё поучиться.";
			close;
		}
		emotion e_gg;
		mes "[Нодор]";
		mes "Эххххххх~";
		next;
		mes "[Нодор]";
		mes "Блин! Прости!";
		mes "Я тут уже давным давно получил письмо от брата, не ознакомишься?";
		next;
		if(select("Давай.:Зачем мне это.") == 2) {
			mes "[Нодор]";
			mes "Тебе нужно больше читать!";
			next;
		}
		else {
			mes "["+strcharinfo(0)+"]";
			mes "*Цк...* Ок...";
			next;
		}
		mes "- Он бросает в вашу сторону скомканное письмо. -";
		next;
		mes "Любимому братцу Нодору.";
		mes " ";
		mes "Братец, это твой брат Нудор. Как поживаешь? Я пишу это письмо, потому что...";
		mes "Мне нужны деньги на еду. У тебя же всегда есть деньги, так помоги, не скупись.";
		next;
		mes "Если откажешь... Надеюсь, ты не забыл последний раз, ты, %$&*@*&%$...";
		mes "- Письмо смято настолько, что дальше невозможно разобрать. -";
		mes "Я всё ещё в Шварцвальте.";
		mes "~ Нудор.";
		next;
		mes "- По прочтении письма у вас возникает нехорошее предчувствие. -";
		next;
		mes "[Нодор]";
		mes "Ху~у~";
		mes "Хмм, у меня всего один брат, и тот постоянно денег просит. Это уже просто моя судьба... T_T";
		next;
		mes "- Плохое предчувствие укрепилось. -";
		next;
		changequest 5098,5099;
		mes "[Нодор]";
		mes "Хэй! Супен-новичок "+strcharinfo(0)+"!";
		next;
		mes "[Нодор]";
		mes "Можешь дать немного моему братцу. Он на дирижабле.";
		close;
	}
	else if (checkquest(5099) >= 0) {
		mes "[Нодор]";
		mes "Уже повидался?";
		next;
		select("Сколько мне нужно ему дать?");
		mes "[Нодор]";
		mes "О, ещё не был"+(Sex?"":"а")+" там?";
		mes "А, дай сколько-нибудь~";
		next;
		mes "[Нодор]";
		mes "100,000 зен будет норм~";
		mes "1,000,000 зен будет норм~";
		mes "10,000,000 зен будет норм~";
		close;
	}
	else if (checkquest(5100) >= 0) {
		if (EXSUPNOV_Q_ == 3) set .@n,300000;
		else if (EXSUPNOV_Q_ == 4) set .@n,1000000;
		else set .@n,100000;
		mes "[Нодор]";
		mes "Эй, ты повстречал"+(Sex?"ся":"ась")+" с братцем? Я получил от него ещё одну весточку.";
		next;
		mes "Бро, кто-то дал мне...";
		mes callfunc("F_InsertComma",.@n)+" зен.";
		mes "Тебе на меня не плевать...";
		mes "~ Нудор";
		next;
		select("Он выглядел голодным...");
		mes "[Нодор]";
		mes "Кстати...";
		mes "Сумма в "+(callfunc("F_InsertComma",.@n))+" зени немного не соответствует моему виду, а?";
		next;
		select("Ну.....");
		mes "[Нодор]";
		mes "Ладно~";
		mes callfunc("F_InsertComma",.@n)+" зени...";
		mes "Не маловато ли ты дал"+(Sex?"":"а")+"? Моя репутация теперь наверняка пострадает.";
		next;
		select("Эй...");
		mes "[Нодор]";
		mes "Ух ты, ты уже немного меня понимаешь?";
		next;
		mes "[Нодор]";
		mes "Ты прав"+(Sex?"":"а")+". Этот тест был на твою способность ладить с людьми и входить в их положение.";
		next;
		mes "[Нодор]";
		mes "Я про твои аппетиты...";
		mes "Хахахаха!";
		next;
		select("Эй, это не...");
		mes "[Нодор]";
		mes "Хмм? Ахх...";
		mes "Чуть не забыл тебе рассказать, как преодолеть предел.";
		next;
		mes "[Нодор]";
		mes "Э-кхем! Я не просто терял время, когда тебя отослал!!";
		next;
		emotion e_omg;
		mes "[Нодор]";
		mes "Что за?! --- а? --- !!";
		next;
		select("??????");
		emotion e_swt;
		mes "[Нодор]";
		mes "Осторожно!! На нас набегают големы! Нам... Надо бежать...!!!";
		next;
		emotion e_omg,1;
		if(select("Чего?!:М?") == 2) {
			mes "- Вы оглядываетесь, но -";
			mes "- позади никого нет. -";
			mes "- Вы заподозреваете... -";
		}
		else {
			mes "- Вы переспрашиваете -";
			mes "- Нодора ещё раз... -";
		}
		next;
		if (SkillPoint) {
			mes "[Нодор]";
			mes "У тебя ещё очки умений не использованы.";
			close;
		}
		specialeffect2 EF_LORD;
		specialeffect2 EF_SIGHTRASHER;
		specialeffect2 EF_STORMGUST;
		erasequest 5100;
		mes "[Нодор]";
		mes "Хаха ------------- !!";
		mes "----- уххахахаха----- !!";
		mes "------------ ййяяй -- !!";
		next;
		nude;
		callfunc "F_ClearJobVar"; // -- Clears all job variables for the current player
		jobchange roclass(eaclass()|EAJL_THIRD);
		set EXSUPNOV_Q,5;	
		emotion e_heh;
		mes "[Нодор]";
		mes "Муаххахахахахаха!!";
		mes "Прекрасно! Испугал"+(Sex?"ся":"ась")+"?";
		next;
		select("... ... ...");
		emotion e_dots,1;
		emotion e_sry;
		mes "[Нодор]";
		mes "Извини, что напугал!";
		mes "Мне просто кажется, что наш долг и миссия, миссия Новичков -- это делать жизнь весёлой.";
		next;
		mes "[Нодор]";
		mes "Ну, что-нибудь изменилось?";
		mes "Чувствуешь лёгкость во всём теле..?";
		mes "Сила течёт по венам?";
		next;
		select("...? ...? ...?");
		emotion e_what,1;
		mes "[Нодор]";
		mes "Ой--- Ё--- (ха,ха,ха)!!";
		next;
		emotion e_hmm;
		mes "[Нодор]";
		mes (Sex?"Приятель":"Подруга")+" --";
		mes "Не хочу тебя обидеть, но ты немного тормоз... Как себя чувствуешь?";
		next;
		if(select("О чём ты вообще?:Дошло!") == 1) {
			mes "[Нодор]";
			mes "Э--- ну ты реально тормоз. Ты только что сломал"+(Sex?"":"а")+" границы.";
			next;
			mes "[Нодор]";
			mes "Теперь сможешь стать сильнее. Ты же об этом мечтал"+(Sex?"":"а")+"?";
			next;
		}
		mes "[Нодор]";
		mes "Ok~!";
		mes "А теперь отправляйся в Мир и живи полноценной и счастливой жизнью Новичка!";
		mes "Давай!";
		close;
	}
	else if (EXSUPNOV_Q == 5) {
		mes "[Нодор]";
		mes "(Дуется) ------------- !!";
		mes "Припёр"+(Sex?"ся":"лась")+" наконец-то.";
		next;
		mes "[Нодор]";
		mes "Я тебе кое-что приготовил в подарок --";
		next;
		mes "[Нодор]";
		mes "Проверь, что у тебя достаточно места в ^FF0000инвентаре^000000, чтобы подарки поместились.";
		mes "И не говори, что я не предупреждал, если что.";
		next;
		if(select("Да-ка проверю.:Всё в порядке.") == 1) {
			mes "[Нодор]";
			mes "Давай-давай.";
			close;
		}
		mes "[Нодор]";
		mes "Это мои старые шмотки, но они ещё как новенькие.";
		if (EXSUPNOV_Q_ == 3) getitem 2522,1; // Undershirt
		else if (EXSUPNOV_Q_ == 4) {
			getitem 2628,1; // Novice Armlet [1]
			getitem 2113,1; // Novice Shield [1]
			getitem 2416,1; // Novice Shoes [1]
			getitem 5119,1; // Super Novice Hat [1]
			getitem 2512,1; // Novice Manteau [1]
			getitem 2340,1; // Novice Breastplate [1]
		}
		else getitem 2340,1; // Novice Breastplate [1]
		next;
		select("Вырос из них, хех?");
		mes "[Нодор]";
		mes "А ну отдавай обратно. Как можно так реагировать на подарок от всего сердца?";
		next;
		mes "[Нодор]";
		mes "Я...";
		mes "Я не могу отобрать то, что уже подарил.";
		next;
		mes "[Нодор]";
		mes "Ещё раз поздравляю!";
		next;
		specialeffect2 EF_RECOVERY;
		mes "[Нодор]";
		mes "Пусть Супер-новичка "+strcharinfo(0)+" будет благословл"+(Sex?"ён":"ена")+" Ангелом-Хранителем!";
		set EXSUPNOV_Q,6;
		close;
	}
	else if (EXSUPNOV_Q > 5) {
		mes "[Нодор]";
		mes "Не забывай, что ты Новичок. Оставайся им всегда! Цель жизни в том, чтобы отлично провести время!";
		close;
	}
	mes "[Рыбачащий новичок]";
	mes "Тсс!";
	mes "Рыбу распугаешь.";
	close;
}

airplane,33,47,5	script	Nudor#sp2	729,{
	if (checkquest(5099) >= 0) {
		mes "[Нудор]";
		mes "Аххх~ Денег на еду нет.";
		mes "Есть охота, а братец когда ещё придёт...";
		next;
		select("Хэй, вы... Мистер Нудор?");
		mes "[Нудор]";
		mes "Ээээ?! Кто спрашивает? Мы знакомы?";
		next;
		mes "- Вы обьясняете Нудору, -";
		mes "- почему вы здесь. -";
		next;
		mes "[Нудор]";
		mes "Аха! Ты приш"+(Sex?"ёл":"ла")+" отсыпать мне наличности вместо братца~";
		next;
		mes "[Нудор]";
		mes "Вауваувау~~ Сколько он передал?";
		next;
		mes "- Глаза Нудора блестят, -";
		mes "- а руки сжаты в кулаки. -";
		mes "- Сколько бы ему дать? -";
		next;
		set .@i, select("У меня нет денег:Дать 100'000 зен:Дать 300'000 зен:Дать 100'000'000 зен:Дать всё, что есть");
		mes "[Нудор]";
		if (.@i == 1) {
			mes "- ревёт -";
			mes "Ненавижу Нодора --- !";
			mes "И тебя тоже --- !";
			close2;
			warp "airplane",70,93;
			end;
		}
		else if (.@i < 5) {
			switch(.@i) {
				case 2: set .@n,100000; break;
				case 3: set .@n,300000; break;
				case 4: set .@n,1000000; break;
			}
			mes "Класс~~ Он передал "+callfunc("F_InsertComma",.@n)+" зенек? Неплохо!";
			next;
			if (Zeny < .@n) {
				mes "[Нудор]";
				mes "Э? А где "+callfunc("F_InsertComma",.@n)+" зен?";
				close;
			}
		}
		else {
			mes "Ох... Бох, ты что, вот так возьмёшь и отдашь все-превсе свои деньги? Серьёзно?";
			next;
			if(select("Нет конечно...:Отдать") == 1) {
				mes "[Нудор]";
				mes "Ша! Вот ведь облом!";
				close;
			}
			if (!Zeny) {
				mes "[Нудор]";
				mes "Э? Да у тебя тоже ни гроша?";
				close;
			}
			set .@n,Zeny;
		}
		mes "[Нудор]";
		mes "Аа~!! "+.@n+" зен! Ай да помощь! Йихаа~";
		mes "Поверить не могу...";
		changequest 5099,5100;
		set Zeny, Zeny - .@n;
		if (.@i < 5) set EXSUPNOV_Q_, .@i;
		else {
			if (.@n > 1000000) set EXSUPNOV_Q_,4;
			else if (.@n > 300000) set EX_SUPNOV_Q_,3;
			else set EXSUPNOV_Q_,2;
		}
		next;
		mes "[Нудор]";
		if (Sex) mes "Ты знал? Ты красавчик~";
		else mes "Должен признаться, ты просто... просто... красотка.";
		next;
		mes "[Нудор]";
		mes "- Такое чувство, что вас надули. -";
		mes "- Нужно вернуться к Нодору. -";
		close;
	}
	else if (checkquest(5100) >= 0) {
		mes "[Нудор]";
		mes "Не могу поверить, что бро послал мне деньжат. Круто~";
		next;
		mes "[Нудор]";
		mes "Уупс!";
		mes strcharinfo(0)+", тсс --- !!";
		next;
		mes "[Нудор]";
		mes "Не говори Нодору~~";
		close;
	}
	mes "[Нудор]";
	mes "Аххх~ Денег нет даже на еду.";
	mes "Сейчас с голоду помру, когда же братец придёт...";
	close;
}
