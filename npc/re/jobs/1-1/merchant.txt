//===== rAthena Script ======================================= 
//= Renewal Merchant Job Change
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Job Change to Merchant Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added support for Baby classes. [Euphy]
//= перевод Akaineko
//============================================================ 
alberta_in,53,43,6	script	Merchant#mer	86,{
	if (Upper == 1) {
		if (Class == Job_Novice_High && (ADVJOB == Job_Whitesmith || ADVJOB == Job_Creator)) {
			mes "[Шеф Мансуу]";
			mes "Сколько лет, сколько зим!";
			mes "Э, да ты больше не при делах?";
			mes "Что случилось?";
			next;
			mes "[Шеф Мансуу]";
			mes "Ё...";
			mes "Ты побывал" + (Sex?"":"а") + " в Вальхалле?! Ты проделал" + (Sex?"":"а") + " далёкий путь...";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Шеф Мансуу]";
				mes "Хммм...";
				mes "Похоже, ты ещё не готов" + (Sex?"":"а") + " стать Торговцем снова. Закончи изучение базовых умений Новичка. .";
				next;
				mes "[Шеф Мансуу]";
				mes "Не беспокойся, для тебя у нас всегда найдётся место Торговца. Возвращайся, как будешь готов" + (Sex?"":"а") + ", ладно?";
				close;
			}
			mes "[Шеф Мансуу]";
			mes "Я думаю, это судьба, что мы встретились ещё раз. Отлично. Позволь мне снова сделать тебя Торговцем!";
			next;
			skill 143,0,0;
			jobchange Job_Merchant_High;
			skill 153,1,0;
			skill 154,1,0;
			skill 155,1,0;
			mes "[Шеф Мансуу]";
			mes "Ах~ Как в старые добрые времена! Ну, удачи!";
			close;
		}else{
			mes "[Шеф Мансуу]";
			mes "^333333*Вздох*^000000";
			mes "Мне так скучно...";
			mes "Когда я услышу что-нибудь от моей любимой?";
			close;
		}
	}
	mes "[Гилдсмен Мансуу]";
	mes "Эй, зачем ты здесь?";
	next;
	switch(select("Я хочу быть Торговцем.:Я хочу узнать больше о Торговцах.:Спросить, что нужно чтобы стать Торговцем.:Ничего.")) {
	case 1:
		mes "[Гилдсмен Мансуу]";
		if(BaseJob != Job_Novice) {
			if(BaseJob == Job_Merchant) {
				mes ".....? Что?";
				mes "Ты уже Торговец. Ё-моё..";
				mes "Ха?! ...Я сейчас должен рассмеяться?!";
			}else{
				mes "Ах? Ты пытаешься пойти двумя путями одновременно?";
				mes "Может тебе лучше посвятить себя твоей настоящей профессии?";
				next;
				mes "[Гилдсмен Мансуу]";
				mes "Ты знаешь, у нас есть профессиональная этика.";
			}
			close;
		}else{
			mes "Хочешь стать Торговцем? Хм...";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Гилдсмен Мансуу]";
				mes "Если хочешь стать Торговцем, тебе нужно выучить Basic Skill полностью или распределить все свободные очки умений.";
				next;
				mes "[Гилдсмен Мансуу]";
				mes "Тебе не кажется, что сначала мы должны научиться основам, чтобы потом работать с деньгами?";
				close;
			}
			mes "[Гилдсмен Мансуу]";
			mes "Хорошо, думаю, ты готов" + (Sex?"":"а") + ": базовые умения изучены тобой полностью.";
			mes "Теперь я разрешаю тебе быть Торговцем.";
			next;
			mes "[Гилдсмен Мансуу]";
			mes "Поздравляю!";
			mes "Мои поздравления со вступлением в гильдию торговцев, коллега! Я предвижу в тебе активного члена нашего общества!";
			callfunc "Job_Change",Job_Merchant;
			getitem 1381,1;	// N_Battle_Axe
			next;
			mes "[Гилдсмен Мансуу]";
			mes "Безусловно, мы нуждаемся в молодых людях, разделяющих наше стремление к великой цели - контролировать 20% мирового обьёма валюты. Что ты об этом думаешь? М?";
			mes "Впрочем, что я говорю... Это означает лишь то, что в результате мы будем иметь много денег. Тебе это известно~";
			close;
		}
	case 2:
		mes "[Гилдсмен Мансуу]";
		mes "Торговец? Если не усложнять, человек, который продаёт барахло и получает с этого выгоду, и есть Торговец.";
		mes "Не самый лучший боец, без специальных атакующих/восстанавливающих умений... но Торговец может покупать вещи по низкой цене и продавать их по высокой и так делать деньги.";
		next;
		mes "[Гилдсмен Мансуу]";
		mes "Да, Торговец имеет одно умение - Mammonite, которым он поражает врага своими деньгами... Мы можем носить всё, кроме луков, посохов и двуручных мечей. Но никто не мешает нам покупать и продавать их.";
		mes "Да... Мы, торговцы, всегда думаем о деньгах, уясняешь?";
		close;
	case 3:
		mes "[Гилдсмен Мансуу]";
		mes "Чтобы стать Торговцем, хотя наша работа по сути - просто продавать и получать деньги, тебе нужно выучить Basic Skill до 9 уровня.";
		next;
		mes "[Гилдсмен Мансуу]";
		mes "Раньше мы взимали входную плату. Добыть те деньги новичку было непросто.";
		mes "Было слишком много людей, не имевших возможности собрать такие деньги.";
		next;
		mes "[Гилдсмен Мансуу]";
		mes "Поэтому недавно было принято решение разрешить любому Новичку, желающему стать Торговцем, стать им.";
		mes "Потому что оставаться в живых - это то, что в силах каждого.";
		close;
	case 4:
		close;
	}
}
