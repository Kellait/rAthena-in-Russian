//===== rAthena Script ======================================= 
//= Renewal Mage Job Change
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Job Change to Mage Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added support for Baby classes. [Euphy]
//= Перевод Akaineko
//============================================================ 
geffen_in,164,124,4	script	Mage Guildsman#mage	123,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) {
			if (Class == Job_Novice_High) {
				mes "[Член Гильдии Магов]";
				mes "О, давно не виделись! Уж не " + (Sex?"мёртв":"мертва") + " ли ты был" + (Sex?"":"а") + "?";
				next;
				mes "[Член Гильдии Магов]";
				mes "А, должно быть, ты переродил" + (Sex?"ся":"ась") + ". Что ж, я рад твоему возвращению.";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					mes "[Член Гильдии Магов]";
					mes "Прости, я думаю, что ты ещё не готов" + (Sex?"":"а") + " изучать магию. Почему бы тебе сначала не закончить обучение базовым навыкам?";
					next;
					mes "[Член Гильдии Магов]";
					mes "Не теряй времени. Чем большему ты научишься, тем лучше будешь подготовлен" + (Sex?"":"а") + " к изучению магии.";
					close;
				}
				mes "[Член Гильдии Магов]";
				mes "Поскольку однажды ты уже сдавал" + (Sex?"":"а") + " вступительные экзамены, я не сомневаюсь в твоей квалифицированности. Ты же хочешь вспомнить свои магические умения прямо сейчас, не так ли?";
				next;
				skill 143,0,0;
				jobchange Job_Mage_High;
				skill 157,1,0;
				mes "[Член Гильдии Магов]";
				mes "Ух ты, выглядишь получше. В любом случае, я верю, что ты будешь отличным магом.";
				close;
			}else{
				mes "[Член Гильдии Магов]";
				mes "Есть ли что-то ещё, чем я могу тебе помочь? Если нет, может, тебе пора приступить к практике? Мир ждёт тебя~!";
				close;
			}
		}else{
			mes "[Член Гильдии Магов]";
			mes "Что, ты заинтересован" + (Sex?"":"а") + " в гильдии Магов? Не хотелось бы говорить такого, но твоё место не здесь.";
			next;
			mes "[Член Гильдии Магов]";
			mes "Не знаю, почему ты до сих пор стоишь передо мной, но я с уверенностью могу сказать, что тебе не быть магом.";
			close;
		}
	}
	mes "[Член Гильдии Магов]";
	if(BaseJob != Job_Novice) {
		if(BaseJob == Job_Mage) {
			mes "Эй, ты ещё не осознал" + (Sex?"":"а") + "? Ты уже Маг, глупыш" + (Sex?"":"ка") + "!";
			mes "Скоро ты осознаешь силу внутри себя, когда научишься создавать пламя силой мысли!";
		}else
			mes "Эй~ Давай. Заканчивай с играми. Ты не можешь стать Магом, потому что уже выбрал" + (Sex?"":"а") + " другую профессию.";
		close;
	}else{
		mes "М?";
		next;
		switch(select("Я хочу стать Магом:Каковы требования, чтобы стать магом?:Ничего, спасибо.")) {
		case 1:
			mes "[Член Гильдии Магов]";
			mes "Хочешь стать Магом? Эх...";
			next;
			mes "[Член Гильдии Магов]";
			if (Sex) {
				mes "Эй, взгляни на себя! Ты милашка~! Правда не в моём вкусе...";
			}else{
				mes "Оо, ты такая горячая, крошка~!";
				mes "Я люблю таких как ты~";
			}
			mes "Значит, говоришь, хочешь быть Магом?";
			next;
			if(select("Я хочу быть Магом.:Ничего, спасибо.") == 2) {
				mes "[Член Гильдии Магов]";
				mes "Что~?! Сразу после того как ты говоришь, что хочешь стать магом, ты меняешь решение?! Будь порешительнее!";
				close;
			}
			mes "[Член Гильдии Магов]";
			if (getskilllv("NV_BASIC") < 9) {
				mes "Ох, друг, твои базовые умения недостаточно развиты.";
				mes "Иди и доучи Basic Skill.";
				close;
			}
			mes "Отлично.. подпиши заявку.... У тебя неплохой почерк! Ага, всё. Этого достаточно.";
			mes "Ты... "+strcharinfo(0)+".";
			next;
			mes "[Член Гильдии Магов]";
			mes "хм, я вижу, ты хорошо потрудил" + (Sex?"ся":"ась") + ". Кажешься немного неуклюжим, а так всё в порядке!";
			mes "Хорошо! Всегда придерживаться основ - самый лучший выбор! Я изменю тебя прямо сейчас.";
			next;
			mes "[Член Гильдии Магов]";
			mes "Ха..! Теперь ты Маг, один из наших коллег!";
			mes "Добро пожаловать в Гильдию Магов, наш новый друг!";
			next;
			mes "[Член Гильдии Магов]";
			mes "'Добро пожаловать в Гильдию Магов~'";
			mes "Поздравляю с обретением членства в Гильдии Магов! Удачи!";
			callfunc "Job_Change",Job_Mage;
			getitem 1639,1;	// N_Rod
			close;
		case 2:
			mes "[Член Гильдии Магов]";
			mes "Хочешь быть Магом, да?";
			mes "Я буду счастлива обьяснить требования такой милой девушке, как ты!";
			next;
			mes "[Член Гильдии Магов]";
			mes "Прежде всего, тебе нужно достичь 10 Профессионального уровня Новичка и полностью выучить 'Basic Skill'.";
			next;
			mes "[Член Гильдии Магов]";
			mes "Раньше был сложный тест на изготовление зелья. Из-за этого мы медленно теряли абитуриентов.";
			mes "Поэтому мы решили принимать всех, кто соответствует базовым требованиям.";
			next;
			mes "[Член Гильдии Магов]";
			mes "Не стесняйся. Просто будь магом!";
			close;
		case 3:
			mes "[Член Гильдии Магов]";
			mes "Ничего...?";
			close;
		}
	}
}
